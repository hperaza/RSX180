;**********************************************************************;
;                                                                      ;
;   This file is part of RSX180, a multi-tasking OS for the Z180.      ;
;   Copyright (C) 1985-2020, Hector Peraza.                            ;
;                                                                      ;
;   This program is free software; you can redistribute it and/or      ;
;   modify it under the terms of the GNU General Public License as     ;
;   published by the Free Software Foundation; either version 2 of     ;
;   the License, or (at your option) any later version.                ;
;                                                                      ;
;   This program is distributed in the hope that it will be useful,    ;
;   but WITHOUT ANY WARRANTY; without even the implied warranty of     ;
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the      ;
;   GNU General Public License for more details.                       ;
;                                                                      ;
;   You should have received a copy of the GNU General Public License  ;
;   along with this program; if not, write to the Free Software        ;
;   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.          ;
;                                                                      ;
;**********************************************************************;

	.Z180

; Note: compile with ZSM4 (M80 reverses chars in 'CO' etc.)

Z80182	equ	1
	include	Z180.INC
	include	DCB.INC
	include	TCB.INC
	include	PCB.INC
	include	SYSFN.INC

	include	SYSCONF.INC

	extrn	SYSSTK,MMUINI,INITKM,ININT,SETVEC,INTON,INTOFF
	extrn	TMRINT,$ASGN,SYSENT,SYSLVL,$PHYDV,$FNPAR,$ALLOC
	extrn	CLRMEM,UPTIME,NVREAD,LDRTCB,TRQST,$POLSZ,$PLIST
	extrn	GO

	extrn	INIT_S,INIT_E

	cseg

;-----------------------------------------------------------------------

; This is the main System Startup routine.
; Initializes everything and starts a few essential tasks before
; passing control to the dispatcher.

START::	di
	ld	sp,SYSSTK	; use SYSSTK in common memory

	IF	F$HALT
	; HALT disables the internal Z182 DMA by default, which stalls disk
	; I/O. In some early/buggy Z182 chips (like the one my P112 has)
	; the ENH182 register cannot be accessed or is not implemented at
	; all, and therefore DMA can't be enabled on HALT. Thus, the
	; dipatcher will use a loop instead of a HALT instruction to wait
	; for an interrupt when there are no tasks ready unless the F$HALT
	; option is specifically enabled by the user in the SYSCONF.INC
	; configuration file.
	in0	a,(enh182)
	set	5,a		; enable DMA on halt
	out0	(enh182),a
	ENDIF

	ld	a,0F0h
	out0	(CBAR),a	; bank 0000-EFFF, common F000-FFFF
	in0	a,(BBR)
	out0	(CBR),a

	ld	hl,($POLSZ)
	ld	a,h
	or	l
	jp	z,uncfg

	ld	hl,0
	ld	bc,128
	call	CLRMEM		; clear RST vector area

	ld	a,-1
	ld	(SYSLVL),a	; we are in system state

	call	NVREAD		; read machine-specific settings
	call	INITKM		; init task manager
	call	MMUINI		; init MMU
	call	ININT		; init interrupt system
	call	INTOFF		; make sure interrupts are off

	ld	de,TMRINT	; timer interrupt routine = int vector 2
	ld	a,2
	call	SETVEC

	ld	hl,UPTIME
	ld	bc,5
	call	CLRMEM		; init uptime

	ld	hl,($PLIST)
p1:	ld	a,h
	or	l
	jr	z,nopar		; no partitions defined!
p2:	push	hl
	pop	ix		; get PCB address into IX
	ld	l,(ix+P.LNK)
	ld	h,(ix+P.LNK+1)
	ld	a,h
	or	l		; last in list?
	jr	nz,p2		; no, keep searching
	bit	PA.SYS,(ix+P.ATTR)
	jr	z,nopar		; user controlled, can't resize
	in0	a,(RAMUBR)
	inc	a
	sub	a,(ix+P.BASE)
	ld	(ix+P.SIZE),a	; resize partition to use the remaining available RAM

nopar:	ld	a,0C3h
	ld	(SYSRST),a	; for INIT task... in the future it will
	ld	hl,SYSENT	;  reside on its own bank
	ld	(SYSRST+1),hl

	ld	(0),a
	ld	hl,CHKTRP##
	ld	(1),hl		; setup illegal instruction trap for kernel

	; initialize physical devices

	call	INTON		; enable timer interrupts to service
	ei			;  any possible device timeouts

	ld	hl,$PHYDV
inidev:	ld	e,(hl)		; get DCB address into DE
	inc	hl
	ld	d,(hl)
	inc	hl
	ld	a,d
	or	e		; end of list?
	jr	z,done		; exit loop if yes
	push	de
	ld	hl,D.EPT
	add	hl,de
	ld	a,(hl)		; get address of dispatch table
	inc	hl
	ld	h,(hl)
	ld	l,a
	call	jphl		; call the driver's initialization entry point
	pop	hl
	jr	inidev

jphl:	jp	(hl)

done:	; assign console output CO: to TT0: (TODO: only if CO: is not loaded)

  IF 0
	ld	de,'TT'
	ld	c,0		; 'TT0:' DE-C = physical
	ld	hl,'CO'
	ld	b,0		; 'CO0:' HL-B = logical (system console device)
	ld	a,N.GBL
	ld	ix,0
	call	$ASGN
  ENDIF

  IF 1
	ld	de,(0081h)
	ld	a,(0083h)
	ld	c,a
	call	$FNDEV##
;;	jp	c,...
	push	hl
	ld	hl,(DEVLB##+3)
	ld	de,U.RED
	add	hl,de
	pop	de
	ld	(hl),e
	inc	hl
	ld	(hl),d
  ENDIF

	ld	hl,0
	ld	(MCRTCB##),hl

	; start loader task - should have been installed and fixed in memory
	; by VMR

	ld	hl,LDRTDB
	call	TRQST		; request loader
	jp	c,noldr
	ld	(LDRTCB),ix

	; start initialization task

	ld	bc,SYSPAR
	call	$FNPAR##	; find system partition
	jp	c,uncfg		; error if not found
	ld	(INITCB+T.PCB),hl ; set pointer to partition in TCB
	ld	de,'TT'
	ld	c,0
	ld	ix,INITCB
	call	$FNDEV##	; get UCB for console device
	jp	c,uncfg
	ld	(INITCB+T.TI),hl
	ld	bc,TCBSZ
	call	$ALLOC		; allocate new TCB for initialization task
	jp	c,uncfg
	push	hl
	pop	ix		; get TCB address into IX
	ex	de,hl
	ld	hl,INITCB
	ld	bc,TCBSZ
	ldir			; copy TCB
	ld	e,(ix+T.PCB)
	ld	d,(ix+T.PCB+1)
	ld	hl,P.BASE
	add	hl,de
	ld	a,(hl)
	push	af		; save bank
	push	de		; push PCB address
	ld	l,(ix+T.END)
	ld	h,(ix+T.END+1)
	ld	e,(ix+T.STRT)
	ld	d,(ix+T.STRT+1)
	or	a
	sbc	hl,de
	ld	de,4095
	add	hl,de
	ld	a,h
	rrca
	rrca
	rrca
	rrca
	and	0Fh		; number of pages
	pop	de		; pop PCB address
	ld	hl,P.SIZE
	add	hl,de
	ld	(hl),a		; (assumes user-controlled partition!)
				; (but this routine will be removed soon anyway)
	ld	c,(ix+T.EPT)	; get entry point into BC
	ld	b,(ix+T.EPT+1)
	ld	e,(ix+T.END)	; get initial top of stack into DE
	ld	d,(ix+T.END+1)
;;	inc	de
	ld	hl,-22
	add	hl,de		; correct for pushed registers
	ld	(ix+T.SP),l	; set the real SP
	ld	(ix+T.SP+1),h
	ex	de,hl		; initial top of stack now in HL
	pop	af
	ld	e,a		; dest bank in E
	dec	hl
	ld	a,b		; setup return address in task's stack
	call	$UPUTB##	;  in user space
	dec	hl
	ld	a,c
	call	$UPUTB##
	call	NEWCTX##	; alloc and link context block, ignore errors
	ld	e,(ix+T.CTX)
	ld	d,(ix+T.CTX+1)
	ld	hl,TX.UID
	add	hl,de
	ld	(hl),1		; set user ID
	inc	hl
	ld	(hl),1		; set group ID
	ld	hl,TX.DIR
	add	hl,de
	ld	bc,9
	call	CLRMEM		; clear current dir
	ld	iy,$RLIST##
	ld	bc,T.ACTL
	call	$LKTCB##	; insert TCB in ready list
	ld	iy,$TLIST##
	ld	bc,T.TCBL
	call	$LKTCB##	; add TCB to the task directory

	; start dispatcher

	jp	GO

uncfg:	call	$DMTRP##
	db	'RSX180 -- System not configured, stopping',0
	di
	halt

noldr:	call	$DMTRP##
	db	'RSX180 -- Loader not installed, stopping',0
	di
	halt

SYSPAR:	db	'SYSPAR'

;-----------------------------------------------------------------------

	dseg

	public	IDDTBL

IDDTBL:	GENDEV			; device-driver table from SYSCONF.INC
	dw	DEVTI##		; pseudo-device TI:
	dw	DEVLB##		; pseudo-device LB:
	dw	DEVSY##		; pseudo-device SY:
	dw	0		; end of table marker

LDRTDB:	db	'LDR...'	; task name
	dw	0,0		; addr and length of command line (not used)
	db	0,0,0		; attr, user, grp (not used)
	db	'CO',0		; terminal device
	dw	0,0		; addr and size of data blk to send (not used)

INITCB:	dw	0		; T.LNK
	dw	0		; T.TCBL
	dw	0		; T.ACTL
	db	(1 SHL TA.PRV) OR (1 SHL TA.REM) OR (1 SHL TA.CKD) ; T.ATTR
	db	(1 SHL TS.ACT)	; T.ST
	db	0		; T.ST+1
	db	200		; T.DPRI
	db	200		; T.PRI
	db	0		; T.SPRI
	db	'INIT  '	; T.NAME
	db	'V6.03 '	; T.VID
	dw	0		; T.CMD
	db	0		; T.IOC
	dw	0		; T.RCVL
	dw	0		; T.OCBL
	dw	0		; T.SAST
	dw	0		; T.ASTL
	dw	0		; T.SVST
	db	0,0,0,0		; T.FLGS
	db	0,0,0,0		; T.WAIT
	dw	0		; T.CTX
	dw	0		; T.TI
	dw	0		; T.LDEV
	dw	0,0		; T.SBLK
	dw	0		; T.NBLK
	dw	0		; T.PCB
	dw	0		; T.CPCB
	dw	INIT_S		; T.STRT
	dw	INIT_E		; T.DEND
	dw	INIT_E		; T.END
	dw	INIT_S		; T.SPT
	dw	0		; T.SP

	end	START
