	.Z80

	public	LOG,LOG10

	extrn	FLD1R,FST0R,FMOVE,FADD,FMUL,FDIV,PLYEVL,?XFORM,IFP
	extrn	FR0,FR1,?FLAG,?DIGRT,?FSCR,ILN10,FPhalf

	cseg

LOG:	ld	a,1
	jr	log_e

LOG10:	xor	a
log_e:	ld	(?FLAG),a	; ?FLAG: 0 = log10(x), 1 = ln(x)
	ld	a,(FR0)
	or	a
	scf
	ret	z		; error if FR0 <= 0
	ret	m
;;	ld	a,(FR0)
	ld	(FR1),a
	sub	40h		; subtract exponent bias
	add	a,a		; double value to get the real exponent
	ld	(?DIGRT),a
	ld	a,(FR0+1)	; get first byte of mantissa
	and	0F0h		; mask first BCD digit
	jr	nz,log1		; branch if not zeo
	inc	a		; A=01h
	jr	log2
log1:	ld	hl,?DIGRT
	inc	(hl)		; fix the real exponent
	ld	a,10h		; A=10h
log2:	ld	(FR1+1),a
	ld	hl,FR1+2+4	; FR1+6 = FR2
	ld	b,5
	xor	a
log3:	ld	(hl),a		; zero the rest (down to FR1+2?)
	dec	hl
	djnz	log3

	call	FDIV		; normalize argument
	ld	hl,SQR10	; sqr(10)
	call	?XFORM		; FR0 = (FR0 - sqr(10)) / (FR0 + sqr(10))
	ld	hl,?FSCR
	call	FST0R		; save reduced argument
	call	FMOVE
	call	FMUL		; square it
	ld	c,10		; number of coefficients
	ld	hl,LOGC		; point to coefficients
	call	PLYEVL		; evaluate polynomial
	ld	hl,?FSCR
	call	FLD1R		; get reduced argument into FR1
	call	FMUL		; then multiply by the polynomial result
	ld	hl,FPhalf	; 0.5
	call	FLD1R
	call	FADD		; add 0.5 to result
	call	FMOVE		; and move it to FR1
	xor	a
	ld	(FR0+1),a
	ld	a,(?DIGRT)	; get orig exponent
	ld	(FR0),a
	or	a
	jp	p,log4
	neg			; if negative, make it positive
	ld	(FR0),a
log4:	call	IFP		; convert orig exponent to float
	ld	a,(?DIGRT)
	or	a
	jp	p,log5
	ld	hl,FR0
	set	7,(hl)		; set sign of converted exponent accordingly
log5:	call	FADD		; add exponent to obtain final result
	ld	a,(?FLAG)	; log10 or ln?
	or	a
	ret	z		; return with CY clear if log10
	ld	hl,ILN10
	call	FLD1R
	call	FDIV		; otherwise divide result by 1/ln(10)
	or	a		; clear CY
	ret

SQR10:	db	040h,003h,016h,022h,077h,066h	;  3.16227766 [sqr(10)]

	; logarithm coefficients

LOGC:	db	03Fh,049h,015h,057h,011h,008h	;  0.4915571108
	db	0BFh,051h,070h,049h,047h,008h	; -0.5170494708
	db	03Fh,039h,020h,057h,061h,095h	;  0.3920576195
	db	0BFh,004h,039h,063h,003h,055h	; -0.0439630355
	db	03Fh,010h,009h,030h,012h,064h	;  0.1009301264
	db	03Fh,009h,039h,008h,004h,060h	;  0.0939080460
	db	03Fh,012h,042h,058h,047h,042h	;  0.1242584742
	db	03Fh,017h,037h,012h,006h,008h	;  0.1737120608
	db	03Fh,028h,095h,029h,071h,017h	;  0.2895297117
	db	03Fh,086h,085h,088h,096h,044h	;  0.8685889644

	end
