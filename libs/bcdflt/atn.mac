	.Z80

	public	ATN

	extrn	?XFORM,FST0R,FMOVE,FADD,FMUL,FDIV,PLYEVL,FLD1R
	extrn	?FLAG,?DIGRT,FR0,?FSCR,DEGRAD

	; Compute arctan of value in floating point register FR0

	cseg

ATN:	xor	a
	ld	(?FLAG),a	; sign flag off
	ld	(?DIGRT),a	; transform flag off
	ld	a,(FR0)
	and	7Fh		; clear sign bit
	cp	40h
	jp	m,atn1		; if |arg| < 1, use series directly
	ld	a,(FR0)		; here if |arg| >= 1
	and	80h
	ld	(?FLAG),a	; remember sign
	ld	hl,?DIGRT
	inc	(hl)		; set transform flag
	ld	a,(FR0)
	and	7Fh		; force plus
	ld	(FR0),a
	ld	hl,F09999	; 0.9999999999
	call	?XFORM		; change arg to = (X - 1) / (X + 1)
atn1:	ld	hl,?FSCR
	call	FST0R		; save X
	call	FMOVE		; FR1 <- X
	call	FMUL		; X * X (square X)
	ret	c		; on overflow, return
	ld	c,11		; number of coefficients
	ld	hl,ATANC	; point to coefficients
	call	PLYEVL		; evaluate polynomial
	ret	c
	ld	hl,?FSCR
	call	FLD1R		; recall X
	call	FMUL		; X * P(X^2)
	ret	c
	ld	a,(?DIGRT)
	or	a		; was arg transformed?
	jr	z,atn2		; no
	ld	hl,ATAN1
	call	FLD1R
	call	FADD		; yes, add arctan(1) = pi/4
	ld	hl,?FLAG
	ld	a,(FR0)		; get org sign
	or	(hl)		; arctan(-X) = arctan(X)
	ld	(FR0),a
atn2:	ld	a,(DEGRAD)
	or	a		; radians or degrees?
	ret	z		; radians, return
	ld	hl,PI180
	call	FLD1R
	jp	FDIV		; degrees, convert to radians

	; arctan coefficients

ATANC:	db	03Eh,016h,005h,044h,049h,000h	;  0.001605444900
	db	0BEh,095h,068h,038h,045h,000h	; -0.009568384500
	db	03Fh,002h,068h,079h,094h,016h	;  0.0268799416
	db	0BFh,004h,092h,078h,090h,080h	; -0.0492789080
	db	03Fh,007h,003h,015h,020h,000h	;  0.0703152000
	db	0BFh,008h,092h,029h,012h,044h	; -0.0892291244
	db	03Fh,011h,008h,040h,009h,011h	;  0.1108400911
	db	0BFh,014h,028h,031h,056h,004h	; -0.1428315604
	db	03Fh,019h,099h,098h,077h,044h	;  0.1999987744
	db	0BFh,033h,033h,033h,031h,013h	; -0.3333333113 

F09999:	db	03Fh,099h,099h,099h,099h,099h	;  0.9999999999
ATAN1:	db	03Fh,078h,053h,098h,016h,034h	;  0.7853981634 [ arctan(1) ]
PI180:	db	03Fh,001h,074h,053h,029h,025h	;  0.0174532925 [pi/180]

	end
