10 REM   *** WRITTEN BY TOM KARZES, CURTIS JR HS, SUDBURY, MA
20 REM   *** MODIFIED BY DAVE AHL, DIGITAL
30 PRINT "YOU ARE IN A LARGE, NEW GOVERNMENT BUILDING WITH 1,000,000"
40 PRINT "ROOMS IN IT.  THE BUILDING IS 100 ROOMS LONG (0-99), 100"
50 PRINT "ROOMS WIDE (0-99), AND 100 FLOORS HIGH (0-99)." \ PRINT 
60 PRINT "A MAD BOMBER HAS HIDDEN A BOMB IN ONE OF THE ROOMS AND"
70 PRINT "HAS JUST PHONED A THREAT THAT THE BOMB IS SET TO GO"
80 PRINT "OFF IN 200 SECONDS.  FORTUNATELY, YOU HAPPEN TO HAVE A BOMB-"
90 PRINT "TRACKER THAT SENDS OUT SIGNALS THAT GET STRONGER AS YOU"
100 PRINT "GET CLOSER TO THE BOMB.  YOU ARE STARTING IN ROOM 0,0,0"
110 PRINT "AND IT TAKES 10 SECONDS TO MOVE TO ANY OTHER ROOM." \ PRINT 
120 RANDOMIZE \ A=INT(100*RND) \ B=INT(100*RND) \ C=INT(100*RND)
130 IF A=0 THEN IF B=0 THEN IF C=0 THEN 120
140 P1=A/100+B+C*100 \ D=0 \ E=0 \ F=0 \ G=0 \ P=0
150 PRINT "SIGNAL","CURRENT ROOM","SEC. INTO","COORDINATES FOR"
160 PRINT "STRENGTH","L , W , H","MISSION","SEARCH (L,W,H)"
170 B$=" ####.##      ##  ##  ##     ###           "
180 PRINT USING B$,10000-ABS(P1-P),D,E,F,G;
190 INPUT D,E,F \ G=G+10 \ P=D/100+E+F*100 \ IF P=P1 THEN 260
200 IF G=200 THEN 220
210 GO TO 270
220 PRINT "BOOOOOOOMMM!!!" \ PRINT "YOU BLEW IT.  THE BUILDING BLEW UP."
230 PRINT  \ PRINT "WANT TO PLAY AGAIN (YES OR NO)"; \ INPUT A$
240 IF A$="NO" THEN 440
250 PRINT  \ PRINT  \ GO TO 120
260 PRINT "BOMB DEACTIVATED AT";G;"SECONDS!!!" \ PRINT  \ GO TO 230
270 IF D>99 THEN 340
280 IF D<0 THEN 340
290 IF E>99 THEN 340
300 IF E<0 THEN 340
310 IF F>99 THEN 380
320 IF F<0 THEN 390
330 GO TO 180
340 PRINT "YOU WALKED OUT A WINDOW ON THE"; \ IF F<4 THEN 400
350 PRINT F;"TH FLOOR!!!"
360 PRINT "YOU WERE KILLED AND";200-G;"SECONDS LATER THE BUILDING"
370 PRINT "BLEW UP!!!" \ PRINT  \ GO TO 230
380 PRINT "YOU ARE NOW";F*9;"FEET IN THE AIR!!!" \ GO TO 360
390 PRINT "YOU ARE NOW -";F*9;"FEET UNDERGROUND!!!" \ GO TO 360
400 ON F GO TO 410,420,430
410 PRINT " 1ST FLOOR!!!" \ GO TO 360
420 PRINT " 2ND FLOOR!!!" \ GO TO 360
430 PRINT " 3RD FLOOR!!!" \ GO TO 360
440 END
