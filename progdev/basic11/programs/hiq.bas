10 REM   *** WRITTEN BY CHARLES LUND, AMERICAN SCHOOL, THE HAGUE, NETHERLANDS
20 REM   *** MODIFIED AND CONVERTED TO RSTS/E BY DAVID AHL, DIGITAL
30 DIM B(70),T(9,9)
40 PRINT "THIS IS THE GAME OF HI-Q" \ PRINT "HERE IS THE BOARD" \ PRINT 
50 PRINT "          !    !    !"
60 PRINT "         13   14   15" \ PRINT 
70 PRINT "          !    !    !"
80 PRINT "         22   23   24" \ PRINT 
90 PRINT "!    !    !    !    !    !    !"
100 PRINT "29  30   31   32   33   34   35" \ PRINT 
110 PRINT "!    !    !    !    !    !    !"
120 PRINT "38  39   40   41   42   43   44" \ PRINT 
130 PRINT "!    !    !    !    !    !    !"
140 PRINT "47  48   49   50   51   52   53" \ PRINT 
150 PRINT "          !    !    !"
160 PRINT "         58   59   60" \ PRINT 
170 PRINT "          !    !    !"
180 PRINT "         67   68   69" \ PRINT 
190 PRINT "TO SAVE TYPING TIME, A COMPRESSED VERSION OF THE GAME BOARD"
200 PRINT "WILL BE USED DURING PLAY.  REFER TO THE ABOVE ONE FOR PEG"
210 PRINT "NUMBERS.  O.K., LET'S BEGIN..."
220 REM   *** SET UP BOARD
230 FOR R=1 TO 9
240 FOR C=1 TO 9
250 IF (R-4)*(R-5)*(R-6)=0 THEN 290
260 IF (C-4)*(C-5)*(C-6)=0 THEN 290
270 T(R,C)=-5
280 GO TO 310
290 IF (R-1)*(C-1)*(R-9)*(C-9)=0 THEN 270
300 T(R,C)=5
310 NEXT C
320 NEXT R
330 T(5,5)=0 \ GOSUB 550
340 REM   *** INPUT MOVE AND CHECK ON LEGALITY
350 FOR W=1 TO 33
360 READ M
370 DATA 13,14,15,22,23,24,29,30,31,32,33,34,35,38,39,40,41
380 DATA 42,43,44,47,48,49,50,51,52,53,58,59,60,67,68,69
390 B(M)=-7 \ NEXT W
400 B(41)=-3
410 PRINT "MOVE WHICH PIECE"; \ INPUT Z
420 IF B(Z)=-7 THEN 440
430 PRINT "ILLEGAL MOVE, TRY AGAIN..." \ GO TO 410
440 PRINT "TO WHERE"; \ INPUT P
450 IF B(P)=0 THEN 430
460 IF B(P)=-7 THEN 430
470 IF Z=P THEN 410
480 IF ((Z+P)/2)=INT((Z+P)/2) THEN 500
490 GO TO 430
500 IF (ABS(Z-P)-2)*(ABS(Z-P)-18)<>0 THEN 430
510 GOSUB 720
520 GOSUB 550
530 GOSUB 980
540 GO TO 410
550 REM   *** PRINT BOARD
560 FOR X=1 TO 9
570 FOR Y=1 TO 9
580 IF (X-1)*(X-9)*(Y-1)*(Y-9)=0 THEN 610
590 IF (X-4)*(X-5)*(X-6)=0 THEN 630
600 IF (Y-4)*(Y-5)*(Y-6)=0 THEN 630
610 REM
620 GO TO 670
630 IF T(X,Y)<>5 THEN 660
640 PRINT TAB(Y*2);"!";
650 GO TO 670
660 PRINT TAB(Y*2);"O";
670 REM
680 NEXT Y
690 PRINT 
700 NEXT X
710 RETURN
720 REM  *** UPDATE BOARD
730 C=1 \ FOR X=1 TO 9
740 FOR Y=1 TO 9
750 IF C<>Z THEN 940
760 IF C+2<>P THEN 810
770 IF T(X,Y+1)=0 THEN 430
780 T(X,Y+2)=5
790 T(X,Y+1)=0 \ B(C+1)=-3
800 GO TO 920
810 IF C+18<>P THEN 850
820 IF T(X+1,Y)=0 THEN 430
830 T(X+2,Y)=5 \ T(X+1,Y)=0 \ B(C+9)=-3
840 GO TO 920
850 IF C-2<>P THEN 890
860 IF T(X,Y-1)=0 THEN 430
870 T(X,Y-2)=5 \ T(X,Y-1)=0 \ B(C-1)=-3
880 GO TO 920
890 IF C-18<>P THEN 940
900 IF T(X-1,Y)=0 THEN 430
910 T(X-2,Y)=5 \ T(X-1,Y)=0 \ B(C-9)=-3
920 B(Z)=-3 \ B(P)=-7
930 T(X,Y)=0 \ GO TO 970
940 C=C+1
950 NEXT Y
960 NEXT X
970 RETURN
980 REM   *** CHECK IF GAME IS OVER
990 F=0
1000 FOR R=2 TO 8
1010 FOR C=2 TO 8
1020 IF T(R,C)<>5 THEN 1200
1030 F=F+1
1040 FOR A=R-1 TO R+1
1050 T=0
1060 FOR B=C-1 TO C+1
1070 T=T+T(A,B)
1080 NEXT B
1090 IF T<>10 THEN 1110
1100 IF T(A,C)<>0 THEN 1320
1110 NEXT A
1120 FOR X=C-1 TO C+1
1130 T=0
1140 FOR Y=R-1 TO R+1
1150 T=T+T(Y,X)
1160 NEXT Y
1170 IF T<>10 THEN 1190
1180 IF T(R,X)<>0 THEN 1320
1190 NEXT X
1200 NEXT C
1210 NEXT R
1220 REM   *** GAME IS OVER
1230 PRINT "THE GAME IS OVER."
1240 PRINT "YOU HAD";F;"PIECES REMAINING"
1250 IF F<>1 THEN 1280
1260 PRINT "BRAVO!  YOU MADE A PERFECT SCORE!!"
1270 PRINT "SAVE THIS PAPER AS A RECORD OF YOUR WORK."
1280 PRINT  \ PRINT "PLAY AGAIN (YES OR NO)"; \ INPUT A$
1290 IF A$="NO" THEN 1330
1300 RESTORE  \ GO TO 230
1310 STOP
1320 RETURN
1330 PRINT  \ PRINT "SO LONG FOR NOW." \ PRINT 
1340 END
