10 REM ROCK SCISSORS PAPER GAME FOR PDP11
20 PRINT "THE GAME OF ROCK, SCISSORS AND PAPER"
30 PRINT 
40 G=0
50 REM GAME NUMBER ROUTINE
60 PRINT "HOW MANY GAMES WOULD YOU LIKE TO PLAY";
70 INPUT Q
80 IF Q<1 GO TO 110
90 IF Q>10 GO TO 110
100 GO TO 160
110 PRINT 
120 PRINT "SORRY, BUT I AM NOT ALLOWED TO PLAY THAT MANY"
130 PRINT "PLEASE ENTER A NEW NUMBER BETWEEN ONE AND TEN"
140 PRINT 
150 GO TO 60
160 RANDOMIZE
170 G=G+1
180 PRINT 
190 PRINT "THIS IS GAME NUMBER";G
200 PRINT 
210 REM RANDOM NUMBER GENERATOR
220 X=INT(3*RND(0)+1)
230 REM PLAYERS INPUT AND VALID ENTRY ROUTINE
240 PRINT "3=ROCK, 2=SCISSORS AND 1=PAPER"
250 PRINT "WHATS YOUR CHOICE";
260 INPUT K
270 IF K=1 GO TO 320
280 IF K=2 GO TO 320
290 IF K=3 GO TO 320
300 PRINT "INVALID ENTRY, TRY AGAIN"
310 GO TO 240
320 PRINT "MY CHOICE IS ";
330 REM MY CHOICE DISPLAY LOGIC
340 IF X=1 GO TO 370
350 IF X=2 GO TO 390
360 IF X=3 GO TO 410
370 PRINT "#1 PAPER"
380 GO TO 440
390 PRINT "#2 SCISSORS"
400 GO TO 440
410 PRINT "#3 ROCK"
420 GO TO 440
430 REM GAME LOGIC
440 IF X=K GO TO 590
450 IF X>K GO TO 560
460 IF X=1 GO TO 510
470 PRINT 
480 PRINT "YOU WIN"
490 H=H+1
500 GO TO 620
510 IF K<>3 GO TO 480
520 PRINT 
530 PRINT "I WIN"
540 C=C+1
550 GO TO 620
560 IF K<>1 GO TO 530
570 IF X<>3 GO TO 530
580 GO TO 480
590 PRINT 
600 PRINT "TIE GAME, NO WINNER"
610 F=F+1
620 IF G<Q GO TO 170
630 REM FINAL SCORE DISPLAY
640 PRINT 
650 PRINT "THE FINAL GAME SCORE IS"
660 PRINT "GAMES THAT I WON";C
670 PRINT "GAMES YOU WON   ";H
680 PRINT "TIE GAMES       ";F
690 REM END OF GAME OCT 2013
700 END
