10 E$=CHR$(27)
20 DEF FNC$(X,Y)=E$&"["&STR$(Y)&";"&STR$(X)&"H"
30 DEF FNS$(A,B)=E$&"["&STR$(A)&";"&STR$(B)&"r"
40 L$=E$&"[2J"
50 N$=E$&"[0m"
60 B$=E$&"[1m"
70 U$=E$&"[4m"
80 F$=E$&"[5m"
90 R$=E$&"[7m"
100 W1$=E$&"#6" \ W0$=E$&"#5"
110 PRINT L$; \ PRINT FNC$(1,10);W1$
120 DIM A%(9,19),B%(7,1),S$(10)
130 S$(0)=' . ' \ S$(1)=' S ' \ S$(2)=' D ' \ S$(3)=' B ' \ S$(4)=' C '
140 PRINT FNC$(6,10);B$;F$;"THIS IS THE GAME BATTLESHIP";N$
150 PRINT FNC$(17,15);" IF YOU NEED INSTRUCTIONS, TYPE 'Y'";
160 INPUT M$
170 PRINT L$;FNC$(1,10);W0$
180 IF M$<>"Y" THEN 530
190 PRINT FNC$(1,1);
200 PRINT 
210 PRINT "BATTLESHIP IS PLAYED ON A 10 X 10 MATRIX, YOU HAVE 8 SHIPS TO PLACE"
220 PRINT "ANYWHERE ON THE MATRIX. THE SHIPS MUST BE IN A STRAIGHT LINE AND"
230 PRINT "MUST OCCUPY THE NUMBER OF SQUARES INDICATED:" \ PRINT 
240 PRINT "TWO SUBMARINES..........2 SQUARES EACH"
250 PRINT "TWO DESTROYERS..........2 SQUARES EACH"
260 PRINT "TWO BATTLESHIPS.........3 SQUARES EACH"
270 PRINT "TWO AIRCRAFT CARRIERS...4 SQUARES EACH"
280 PRINT 
290 PRINT "TO SINK A SHIP, YOU MUST HIT EVERY SQUARE OCCUPIED BY THAT SHIP"
300 PRINT 
310 PRINT "INPUT COORDINATES AS WHOLE NUMBERS. FOR EXAMPLE: ENTER 159 FOR 15,9"
320 PRINT  \ PRINT  \ PRINT 
330 PRINT "TYPE A RETURN TO GET MORE EXPLANATION" \ INPUT A$
340 PRINT FNC$(1,1);L$;
350 PRINT 
360 PRINT "***************STRATEGY HINTS****************"
370 PRINT 
380 PRINT 
390 PRINT "IT WILL BE TO YOUR ADVANTAGE TO SPACE YOUR SHIPS SO THAT"
400 PRINT "NONE WILL BE FOUND WHILE TRYING TO SINK ANOTHER SHIP."
410 PRINT "HOWEVER, YOU SHOULD NOT ASSUME THAT THE MACHINE WILL DO THIS."
420 PRINT 
430 PRINT "THE MORE METHODICAL YOUR SEARCH IS, THE BETTER YOUR CHANCES."
440 PRINT "YOU SHOULD USE THE BOARDS PRINTED OUT TO KEEP CLOSE TRACK OF"
450 PRINT "WHERE YOU HAVE SHOT OR HIT. THE MACHINE WILL NOT STOP YOU"
460 PRINT "FROM WASTING SHOTS, THAT'S YOUR OWN STUPIDITY."
470 PRINT "ALSO, NOTE THAT ANY ILLEGAL SHOT, SUCH AS A NEGATIVE NUMBER"
480 PRINT "OR ANY NUMBER <100 OR >199, IS LOST."
490 PRINT 
500 PRINT  \ PRINT 
510 PRINT "TYPE A RETURN WHEN YOU ARE READY TO BEGIN THE GAME" \ INPUT A$
520 PRINT L$;
530 PRINT FNS$(18,24);FNC$(1,2);W1$
540 PRINT FNC$(1,1);W1$
550 PRINT FNC$(14,1);U$;'MINC Battleship';N$
560 PRINT FNC$(6,2);'Your board' \ PRINT FNC$(27,2);'My board' \ PRINT FNC$(1,24);
570 FOR Y=3 TO 15 \ PRINT FNC$(40,Y);R$;'|';N$ \ NEXT Y
580 PRINT FNC$(1,24);
590 RANDOMIZE
600 FOR M%=0% TO 19%
610 FOR N%=0% TO 9%
620 A%(N%,M%)=1%
630 NEXT N%
640 NEXT M%
650 B%(0,0)=8% \ B%(0,1)=2% \ B%(1,0)=8% \ B%(1,1)=3% \ B%(2,0)=8% \ B%(2,1)=4% \ B%(3,0)=8% \ B%(3,1)=5%
660 B%(4,0)=7% \ B%(4,1)=6% \ B%(5,0)=7% \ B%(5,1)=7% \ B%(6,0)=6% \ B%(6,1)=8% \ B%(7,0)=6% \ B%(7,1)=9%
670 PRINT FNC$(1,16);'                                                                                '
680 M1%=0%
690 M2%=9%
700 M9%=0%
710 GOSUB 3120
720 M9%=40% \ GOSUB 3120 \ M9%=0%
730 FOR X%=7% TO 0% STEP -1%
740 P%=INT(RND(0)*B%(X%,0%))
750 Q%=INT(RND(0)*10)
760 IF RND(0)>.5 THEN 2760
770 FOR Y%=1% TO (10%-B%(X%,0%))
780 F%=P%+Y%-1%
790 IF A%(F%,Q%)>1% THEN 740
800 E%=Q%+1%
810 IF E%>9% THEN 830
820 IF A%(F%,E%)>1% THEN 740
830 E%=Q%-1%
840 IF E%<0% THEN 860
850 IF A%(F%,E%)>1% THEN 740
860 NEXT Y%
870 FOR Y%=1% TO (10%-B%(X%,0%))
880 F%=P%+Y%-1%
890 A%(F%,Q%)=B%(X%,1%)*A%(F%,Q%)
900 NEXT Y%
910 NEXT X%
920 IF M$<>"MAGIC" THEN  GO TO 960
930 FOR Y%=0% TO 4% \ FOR X%=0% TO 8% \ PRINT A%(X%,Y%); \ NEXT X% \ PRINT A%(9%,Y%),
940 FOR X%=0% TO 9% \ PRINT A%(X%,Y%+5%); \ NEXT X% \ PRINT  \ NEXT Y% \ INPUT M$
950 PRINT  \ PRINT  \ PRINT  \ PRINT  \ PRINT  \ PRINT 
960 PRINT FNC$(1,24);"INPUT THE ENDPOINTS OF YOUR SHIPS AS YOU ARE ASKED FOR THEM"
970 FOR M%=2% TO 9%
980 IF M%<4% THEN 1140
990 IF M%<6% THEN 1160
1000 IF M%<8% THEN 1180
1010 PRINT "AIRCRAFT CARRIER";
1020 INPUT N%,P%
1030 IF N%<100% THEN 1200
1040 IF N%>199% THEN 1200
1050 N1%=INT(N%/10%)
1060 N%=INT(N%-10%*N1%)
1070 IF P%<100% THEN 1200
1080 IF P%>199% THEN 1200
1090 P1%=INT(P%/10%)
1100 P%=INT(P%-P1%*10%)
1110 Y%=M%-2%
1120 IF SQR((N%-P%)^2%+(N1%-P1%)^2%)<>9%-B%(Y%,0%) THEN 1200
1130 GO TO 1220
1140 PRINT "SUBMARINE";
1150 GO TO 1020
1160 PRINT "DESTROYER";
1170 GO TO 1020
1180 PRINT "BATTLESHIP";
1190 GO TO 1020
1200 PRINT "ILLEGAL ENTRY, TRY AGAIN"
1210 GO TO 980
1220 IF P%>N% THEN 2910
1230 Y%=1%
1240 IF P1%>N1% THEN 2930
1250 Z%=1%
1260 FOR Q%=P% TO N% STEP Y%
1270 FOR F%=P1% TO N1% STEP Z%
1280 IF A%(Q%,F%)>1% THEN 1200
1290 NEXT F%
1300 NEXT Q%
1310 FOR Q%=P% TO N% STEP Y%
1320 FOR F%=P1% TO N1% STEP Z%
1330 A%(Q%,F%)=M%
1340 PRINT FNC$(10%+2%*Q%,5%+F%-10%);S$(ABS(A%(Q%,F%))/2%)
1350 NEXT F%
1360 NEXT Q%
1370 PRINT FNC$(1,24);
1380 NEXT M%
1390 GOSUB 2950
1400 M1%=10%
1410 M2%=19%
1420 Q%=1%
1430 PRINT FNC$(39,16);"    " \ PRINT FNC$(1,18);"               YOU HAVE 5 SHOTS         " \ PRINT FNC$(1,19);
1440 FOR X%=1% TO 5% \ PRINT FNC$(1,18+X%);"                                       " \ NEXT X%
1450 FOR X%=1% TO 5%
1460 PRINT FNC$(1,18+X%);"SHOT #"+STR$(X%)+" " \ PRINT FNC$(12,18+X%);
1470 INPUT N%
1480 N%=N%-100%
1490 IF N%<0% THEN 1530
1500 IF N%>99% THEN 1530
1510 E%=0%
1520 GOSUB 1550
1530 NEXT X%
1540 GO TO 2080
1550 P%=INT(N%/10%)
1560 N%=INT(N%-P%*10%)
1570 IF E%=10% THEN 2350
1580 A%(N%,P%)=-1%*ABS(A%(N%,P%))
1590 IF ABS(A%(N%,P%))>1% THEN 1680
1600 PRINT FNC$(39,16);B$;"MISS";N$
1610 IF P%<10% THEN PRINT FNC$(50%+2%*N%,5%+P%);" *"
1620 IF P%>9% THEN PRINT FNC$(10%+2%*N%,5%+P%-10%);" *"
1630 IF E%=0% THEN 1670
1640 IF Q%=-2% THEN 2240
1650 IF Q%=1% THEN 1670
1660 GOSUB 2520
1670 RETURN
1680 PRINT FNC$(39,16);B$;F$;CHR$(7)+CHR$(7)+" HIT";N$
1690 IF P%<10% THEN PRINT FNC$(50%+2%*N%,5%+P%);F$;" H";N$ \ H9%=5%+P% \ H8%=50%+2%*N%
1700 IF P%>9% THEN PRINT FNC$(10%+2%*N%,5%+P%-10%);F$;" H";N$ \ H9%=5%+P%-10% \ H8%=10%+2%*N%
1710 GOSUB 2950 \ REM UPDATE THE SCORE
1720 PRINT FNC$(1,19);
1730 IF E%=0% THEN 1850
1740 IF Q%<>1% THEN 1810
1750 B%(4%,0%)=ABS(A%(N%,P%))
1760 B%(2%,0%)=P%*10%+N%
1770 B%(3%,0%)=ABS(A%(N%,P%))
1780 Q%=-1%
1790 GOSUB 2520
1800 IF Q%=-1% THEN 1830
1810 IF ABS(A%(N%,P%))<>B%(3%,0%) THEN 1830
1820 Q%=-2%
1830 M%=ABS(A%(N%,P%))-2%
1840 B%(M%,1%)=-1%*ABS(B%(M%,1%))
1850 FOR Y%=E% TO E%+9%
1860 FOR M%=0% TO 9%
1870 IF A%(M%,Y%)=ABS(A%(N%,P%)) THEN 1670
1880 NEXT M%
1890 NEXT Y%
1900 PRINT FNC$(39,16);B$;F$;R$;CHR$(7)+CHR$(7)+"SINK";N$
1910 PRINT FNC$(H8%+1%,H9%);B$;F$;R$;'S';N$
1920 PRINT FNC$(1,19);
1930 IF E%=0% THEN 2020
1940 M%=ABS(A%(N%,P%))-2%
1950 B%(M%,1%)=ABS(B%(M%,1%))
1960 M%=B%(4%,0%)-2%
1970 IF B%(4%,0%)<>B%(M%,1%) THEN 2240
1980 FOR M%=0% TO 7%
1990 IF B%(M%,1%)<0% THEN 2400
2000 NEXT M%
2010 Q%=1%
2020 FOR M%=0% TO 9%
2030 FOR Y%=E% TO E%+9%
2040 IF A%(M%,Y%)>1% THEN 1670
2050 NEXT Y%
2060 NEXT M%
2070 GO TO 2570
2080 E%=10%
2090 PRINT FNC$(39,16);"    "
2100 PRINT FNC$(41,18);"               I HAVE 5 SHOTS          "
2110 FOR X%=1% TO 5% \ PRINT FNC$(55,18+X%);"               " \ NEXT X%
2120 FOR X%=1% TO 5%
2130 IF Q%>0% THEN 2280
2140 B%(1%,0%)=B%(1%,0%)+P1%
2150 N%=B%(1%,0%)
2160 GOSUB 1550
2170 NEXT X%
2180 GO TO 1430
2190 IF Q%=-2% THEN 2250
2200 IF Q%=1% THEN 2220
2210 GOSUB 2520
2220 X%=X%-1%
2230 GO TO 1670
2240 X%=X%+1%
2250 P1%=-1%*P1%
2260 B%(1%,0%)=B%(2%,0%)
2270 GO TO 2220
2280 M%=INT(RND(0)*5%)*2%
2290 N%=INT(RND(0)*10)+10%
2300 Y%=(-1%)^N%
2310 IF Y%>0% THEN 2330
2320 M%=M%-Y%
2330 N%=M%+N%*10%
2340 GO TO 2160
2350 IF P%*10%+N%<100% THEN 2190
2360 IF P%*10%+N%>199% THEN 2190
2370 IF A%(N%,P%)<1% THEN 2190
2380 PRINT FNC$(41,18+X%);"              MY SHOT #"+STR$(X%)+"  "+STR$(P%*10%+N%)
2390 GO TO 1580
2400 B%(3%,0%)=B%(M%,1%)
2410 FOR Y%=E% TO E%+9%
2420 FOR M%=0% TO 9%
2430 IF A%(M%,Y%)=B%(3%,0%) THEN 2480
2440 NEXT M%
2450 NEXT Y%
2460 Q%=1%
2470 GO TO 2130
2480 B%(2%,0%)=Y%*10%+M%
2490 Q%=-1%
2500 GOSUB 2520
2510 GO TO 1670
2520 P1%=INT(RND(0)*2%)*9%+1%
2530 B%(1%,0%)=B%(2%,0%)
2540 IF RND(0)>.5 THEN 2560
2550 P1%=-1%*P1%
2560 RETURN
2570 FOR X%=1% TO 60%
2580 PRINT CHR$(7);
2590 NEXT X%
2600 PRINT FNC$(39,16);"    "
2610 FOR X%=18% TO 24% \ PRINT FNC$(1,X%);'                                                                              ' \ NEXT X%
2620 PRINT FNC$(1,18);W1$
2630 IF E%=0% THEN 2740
2640 PRINT FNC$(19,18);B$;F$;R$;"I ";N$
2650 PRINT FNC$(21,18);B$;F$;R$;"WIN";N$
2660 M1%=0%
2670 M2%=9%
2680 M4%=-1%
2690 M1%=10%
2700 M2%=19%
2710 PRINT  \ PRINT  \ PRINT 'DO YOU WISH TO PLAY AGAIN [Y/N]';
2720 INPUT Y$ \ IF Y$='Y' THEN 110
2730 GO TO 3190
2740 PRINT FNC$(17,18);B$;F$;R$;"YOU ";N$
2750 GO TO 2650
2760 FOR Y%=1% TO (10%-B%(X%,0%))
2770 F%=P%+Y%-1%
2780 IF A%(Q%,F%)>1% THEN 740
2790 E%=Q%+1%
2800 IF E%>9% THEN 2820
2810 IF A%(E%,F%)>1% THEN 740
2820 E%=Q%-1%
2830 IF E%<0% THEN 2850
2840 IF A%(E%,F%)>1% THEN 740
2850 NEXT Y%
2860 FOR Y%=1% TO (10%-B%(X%,0%))
2870 F%=P%+Y%-1%
2880 A%(Q%,F%)=B%(X%,1%)*A%(Q%,F%)
2890 NEXT Y%
2900 GO TO 910
2910 Y%=-1%
2920 GO TO 1240
2930 Z%=-1%
2940 GO TO 1260
2950 M5%=10%
2960 M6%=19%
2970 GOSUB 3040
2980 PRINT FNC$(10,16);STR$(M4%)+" HITS OUT OF 22"
2990 M5%=0%
3000 M6%=9%
3010 GOSUB 3040
3020 PRINT FNC$(50,16);STR$(M4%)+" HITS OUT OF 22"
3030 RETURN
3040 M4%=0%
3050 FOR M7%=M5% TO M6%
3060 FOR N9%=0% TO 9%
3070 IF A%(N9%,M7%)>-2% THEN 3090
3080 M4%=M4%+1%
3090 NEXT N9%
3100 NEXT M7%
3110 RETURN
3120 PRINT FNC$(1,24);
3130 PRINT FNC$(11+M9%,4);'0 1 2 3 4 5 6 7 8 9' \ REM PRINT M7;
3140 FOR M7%=M1% TO M2%
3150 PRINT FNC$(7%+M9%,5%+M7%-M1%);STR$(M7%+10%-M1%)+" "
3160 PRINT FNC$(10%+M9%,5%+M7%-M1%);' . . . . . . . . . .'
3170 NEXT M7%
3180 RETURN
3190 PRINT FNS$(1,24);L$;FNC$(0,0)
3200 END
