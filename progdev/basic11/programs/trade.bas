10 REM   *** CONVERTED TO RSTS/E BY DAVID AHL, DIGITAL
20 DIM F(4)
30 RANDOMIZE
40 GOSUB 370
50 LET I=600
60 PRINT "DO YOU WISH TO TRADE FURS?"
70 GOSUB 1510
80 IF B$="YES" THEN 110
90 IF B$="NO" THEN 1700
100 GOSUB 1510
110 PRINT 
120 PRINT "YOU HAVE $";I;"SAVINGS."
130 PRINT "AND 190 FURS TO BEGIN THE EXPEDITION."
140 PRINT 
150 PRINT "YOUR 190 FURS ARE DISTRIBUTED AMONG THE FOLLOWING"
160 PRINT "KINDS OF PELTS: MINK, BEAVER, ERMINE AND FOX."
170 GOSUB 1650
180 RESTORE 
190 FOR J=1 TO 4
200 READ B$
210 PRINT 
220 PRINT "HOW MANY ";B$;" PELTS DO YOU HAVE";
230 INPUT F(J)
240 LET F(0)=F(1)+F(2)+F(3)+F(4)
250 IF F(0)=190 THEN 440
260 IF F(0)>190 THEN 290
270 NEXT J
280 GO TO 440
290 PRINT 
300 PRINT "YOU MAY NOT HAVE THAT MANY FURS."
310 PRINT "DO NOT TRY TO CHEAT.  I CAN ADD."
320 PRINT "YOU MUST START AGAIN."
330 GO TO 40
340 PRINT 
350 PRINT "DO YOU WANT TO TRADE FURS NEXT YEAR?"
360 GO TO 70
370 PRINT "YOU ARE THE LEADER OF A FRENCH FUR TRADING EXPEDITION IN "
380 PRINT "1776 LEAVING THE LAKE ONTARIO AREA TO SELL FURS AND GET"
390 PRINT "SUPPLIES FOR THE NEXT YEAR.  YOU HAVE A CHOICE OF THREE"
400 PRINT "FORTS AT WHICH YOU MAY TRADE.  THE COST OF SUPPLIES"
410 PRINT "AND THE AMOUNT YOU RECEIVE FOR YOUR FURS WILL DEPEND"
420 PRINT "ON THE FORT THAT YOU CHOOSE."
430 RETURN
440 PRINT "DO YOU WANT TO TRADE YOUR FURS AT FORT 1, FORT 2,"
450 PRINT "OR FORT 3?  FORT 1 IS FORT HOCHELAGA (MONTREAL)"
460 PRINT "AND IS UNDER THE PROTECTION OF THE FRENCH ARMY."
470 PRINT "FORT 2 IS FORT STADACONA (QUEBEC) AND IS UNDER THE"
480 PRINT "PROTECTION OF THE FRENCH ARMY.  HOWEVER, YOU MUST"
490 PRINT "MAKE A PORTAGE AND CROSS THE LACHINE RAPIDS."
500 PRINT "FORT 3 IS FORT NEW YORK AND IS UNDER DUTCH CONTROL."
510 PRINT "YOU MUST CROSS THROUGH IROQUOIS LAND."
520 PRINT "ANSWER 1, 2, OR 3."
530 INPUT B
540 IF B=1 THEN 580
550 IF B=2 THEN 650
560 IF B=3 THEN 720
570 GO TO 520
580 PRINT "YOU HAVE CHOSEN THE EASIEST ROUTE.  HOWEVER, THE FORT"
590 PRINT "IS FAR FROM ANY SEAPORT.  THE VALUE"
600 PRINT "YOU RECEIVE FOR YOUR FURS WILL BE LOW AND THE COST"
610 PRINT "OF SUPPLIES HIGHER THAN AT FORTS STADACONA OR NEW YORK."
620 GOSUB 1500
630 IF B$="YES" THEN 520
640 GO TO 790
650 PRINT "YOU HAVE CHOSEN A HARD ROUTE.  IT IS, IN COMPARSION,"
660 PRINT "HARDER THAN THE ROUTE TO HOCHELAGA BUT EASIER THAN"
670 PRINT "THE ROUTE TO NEW YORK.  YOU WILL RECEIVE AN AVERAGE VALUE"
680 PRINT "FOR YOUR FURS AND THE COST OF YOUR SUPPLIES WILL BE AVERAGE"
690 GOSUB 1500
700 IF B$="YES" THEN 520
710 GO TO 880
720 PRINT "YOU HAVE CHOSEN THE MOST DIFFICULT ROUTE.  AT"
730 PRINT "FORT NEW YORK YOU WILL RECEIVE THE HIGHEST VALUE"
740 PRINT "FOR YOUR FURS.  THE COST OF YOUR SUPPLIES"
750 PRINT "WILL BE LOWER THAN AT ALL THE OTHER FORTS."
760 GOSUB 1500
770 IF B$="YES" THEN 520
780 GO TO 1190
790 LET I=I-160
800 PRINT 
810 LET M1=INT((.2*RND+.7)*100+.5)/100
820 LET E1=INT((.2*RND+.65)*10^2+.5)/10^2
830 LET B1=INT((.2*RND+.75)*10^2+.5)/10^2
840 LET D1=INT((.2*RND+.8)*10^2+.5)/10^2
850 PRINT "SUPPLIES AT FORT HOCHELAGA COST $150.00"
860 PRINT "YOUR TRAVEL EXPENSES TO HOCHELAGA WERE $10.00"
870 GO TO 1560
880 LET I=I-140
890 PRINT 
900 LET M1=INT((.3*RND+.85)*10^2+.5)/10^2
910 LET E1=INT((.15*RND+.8)*10^2+.5)/10^2
920 LET B1=INT((.2*RND+.9)*10^2+.5)/10^2
930 LET P=INT(10*RND)+1
940 IF P<=2 THEN 980
950 IF P<=6 THEN 1040
960 IF P<=8 THEN 1060
970 IF P<=10 THEN 1110
980 LET F(2)=0
990 PRINT "YOUR BEAVER WERE TOO HEAVY TO CARRY ACROSS"
1000 PRINT "THE PORTAGE.  YOU HAD TO LEAVE THE PELTS BUT FOUND"
1010 PRINT "THEM STOLEN WHEN YOU RETURNED"
1020 GOSUB 1160
1030 GO TO 1580
1040 PRINT "YOU ARRIVED SAFELY AT FORT STADACONA"
1050 GO TO 1140
1060 GOSUB 1650
1070 PRINT "YOUR CANOE UPSET IN THE LACHINE RAPIDS.  YOU"
1080 PRINT "LOST ALL YOUR FURS"
1090 GOSUB 1160
1100 GO TO 1610
1110 LET F(4)=0
1120 PRINT "YOUR FOX PELTS WERE NOT CURED PROPERLY."
1130 PRINT "NO ONE WILL BUY THEM."
1140 GOSUB 1160
1150 GO TO 1560
1160 PRINT "SUPPLIES AT FORT STADACONA COST $125.00"
1170 PRINT "YOUR TRAVEL EXPENSES TO STADACONA WERE $15.00"
1180 RETURN
1190 LET I=I-105
1200 PRINT 
1210 LET M1=INT((.15*RND+1.05)*10^2+.5)/10^2
1220 LET E1=INT((.15*RND+.95)*10^2+.5)/10^2
1230 LET B1=INT((.25*RND+1)*10^2+.5)/10^2
1240 LET D1=INT((.25*RND+1.1)*10^2+.5)/10^2
1250 LET P=INT(10*RND)+1
1260 IF P<=2 THEN 1300
1270 IF P<=6 THEN 1340
1280 IF P<=8 THEN 1370
1290 IF P<=10 THEN 1420
1300 PRINT "YOU WERE ATTACKED BY A PARTY OF IROQUOIS."
1310 PRINT "ALL PEOPLE IN YOUR TRADING GROUP WERE"
1320 PRINT "KILLED.  THIS ENDS THE GAME."
1330 STOP
1340 PRINT "YOU WERE LUCKY.  YOU ARRIVED SAFELY"
1350 PRINT "AT FORT NEW YORK."
1360 GO TO 1460
1370 GOSUB 1650
1380 PRINT "YOU NARROWLY ESCAPED AN IROQUOIS RAIDING PARTY."
1390 PRINT "HOWEVER, YOU HAD TO LEAVE ALL YOUR FURS BEHIND."
1400 GOSUB 1480
1410 GO TO 1610
1420 LET B1=B1/2
1430 LET M1=M1/2
1440 PRINT "YOUR MINK AND BEAVER WERE DAMAGED ON YOUR TRIP."
1450 PRINT "YOU RECEIVE ONLY HALF THE CURRENT PRICE FOR THESE FURS."
1460 GOSUB 1480
1470 GO TO 1560
1480 PRINT "SUPPLIES AT NEW YORK COST $80.00"
1490 RETURN
1500 PRINT "DO YOU WANT TO TRADE AT ANOTHER FORT?"
1510 PRINT "ANSWER YES OR NO",
1520 A=SYS(7,1) \ REM FORCE UPPERCASE
1530 INPUT B$
1540 A=SYS(7,0)
1550 RETURN
1560 PRINT 
1570 PRINT "YOUR BEAVER SOLD FOR $";B1*F(2);
1580 PRINT "YOUR FOX SOLD FOR $";D1*F(4)
1590 PRINT "YOUR ERMINE SOLD FOR $";E1*F(3);
1600 PRINT "YOUR MINK SOLD FOR $";M1*F(1)
1610 LET I=M1*F(1)+B1*F(2)+E1*F(3)+D1*F(4)+I
1620 PRINT 
1630 PRINT "YOU NOW HAVE $";I;" INCLUDING YOUR PREVIOUS SAVINGS"
1640 GO TO 340
1650 FOR J=1 TO 4
1660 LET F(J)=0
1670 NEXT J
1680 RETURN
1690 DATA "MINK","BEAVER","ERMINE","FOX"
1700 END
