;**********************************************************************;
;                                                                      ;
;   This file is part of RSX180, a multi-tasking OS for the Z180.      ;
;   Copyright (C) 1985-2023, Hector Peraza.                            ;
;                                                                      ;
;   This program is free software; you can redistribute it and/or      ;
;   modify it under the terms of the GNU General Public License as     ;
;   published by the Free Software Foundation; either version 2 of     ;
;   the License, or (at your option) any later version.                ;
;                                                                      ;
;   This program is distributed in the hope that it will be useful,    ;
;   but WITHOUT ANY WARRANTY; without even the implied warranty of     ;
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the      ;
;   GNU General Public License for more details.                       ;
;                                                                      ;
;   You should have received a copy of the GNU General Public License  ;
;   along with this program; if not, write to the Free Software        ;
;   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.          ;
;                                                                      ;
;**********************************************************************;

	NAME	('CONNECT')
	TITLE	'RSX180 Task Connect, Exit and Emit Status'

;----------------------------------------------------------------------;
;                                                                      ;
;	Task Connect, Exit and Emit Status routines.                   ;
;                                                                      ;
;----------------------------------------------------------------------;

	.Z80

	include	ERRORS.INC
	include	TCB.INC
	include	PCB.INC
	include	AST.INC
	include	TKN.INC

	cseg

	public	NEWOCB		; allocate and init new OCB
	public	$CONN		; connect to task
	public	LNKOCB		; link OCB to offspring task
	public	EMITST,EMTST	; emit status
	public	EXIT		; exit task with status
	public	DISCON		; disconnect from all tasks
	public	ESBSIZ		; get status block size according to format
	public	STORST		; store status in user space

	extrn	$CTCB,$ALLOC,$FREE,$SBANK,$MCOPY,QAST,$UCBDN
	extrn	SETFLG,CLRFLG,TKILL,$RLIST,$WLIST

;-----------------------------------------------------------------------

; Connect to task.

; Input:
;   HL = valid TCB address of offspring task
;   D  = exit status format (0=short, 1=long)
;   E  = flag number to set when offspring task emits status.
;   BC = address of Exit Status Block (ESB) in parent's address space.

$CONN:	push	hl
	pop	ix		; IX = offspring TCB
	bit	TA.CLI,(ix+T.ATTR)
	ld	a,E.BADOP	; error if trying to connect to a CLI
	scf
	ret	nz
	bit	TS.ACT,(ix+T.ST)
	ld	a,E.TNAC	; error if offspring task is not active
	scf
	ret	z
	call	NEWOCB		; create new OCB
	ret	c

	; continue below

; here with:
;   HL = offspring task TCB address
;   IX = OCB address

LNKOCB:	ld	de,T.OCBL
	add	hl,de
con0:	ld	e,(hl)		; search for end of OCB list
	inc	hl
	ld	d,(hl)
	dec	hl
	ld	a,d
	or	e
	ex	de,hl
	jr	nz,con0
	ex	de,hl
	push	ix
	pop	de
	ld	(hl),e		; set OCB in offspring task
	inc	hl
	ld	(hl),d
	ret

; Allocate and initialize a new OCB.

; Input:
;   HL = offspring task TCB address
;   D  = exit status format (0=short, 1=long)
;   E  = flag number to set when offspring task emits status.
;   BC = address of Exit Status Block (ESB) in parent's address space.
; Returns:
;   IX = OCB address
; Preserves HL

NEWOCB:	push	hl		; push offspring TCB
	push	de		; flag
	push	bc		; parent ESB
	ld	bc,OCBSZ
	call	$ALLOC		; allocate OCB
	jr	c,nocb
	push	hl
	pop	ix		; IX = OCB
	ld	hl,($CTCB)
	ld	(ix),0		; clear next pointer
	ld	(ix+1),0
	ld	(ix+O.PTCB),l	; set parent's TCB in OCB
	ld	(ix+O.PTCB+1),h
	pop	bc
	ld	(ix+O.ESB),c	; set ESB address
	ld	(ix+O.ESB+1),b
	pop	de
	ld	(ix+O.EFN),e	; set event flag number in OCB
	ld	(ix+O.FMT),d	; set format
	ld	c,l
	ld	b,h
	call	CLRFLG		; clear event flag, preserves IX
	pop	hl		; pop offspring TCB
	ret	nc		; return on success
	push	af
	push	ix
	pop	hl
	ld	de,OCBSZ
	call	$FREE		; else free the OCB
	pop	af		; and return the error code
	ret

nocb:	pop	bc
	pop	de
	pop	hl
	ret

;-----------------------------------------------------------------------

; Exit task with status.

; Input:
;   HL = status.
; This routine is called by a task via SYSENT.

; TODO:
; - cleanup should be done in TKILL, so OCBs get freed also after abort
;   (update: OCBs are now reconnected to TKTN).
; - Abort should also send a status back to parent (E.ABRT) (update: see
;   above)
; - Parent should keep an offspring count to break connections in case
;   parent finishes before offspring

EXIT:	call	EMITST		; emit status
	ld	bc,($CTCB)	;  and exit
	ld	e,TK.NONE	; normal exit
	jp	TKILL		; TKILL never returns for the current
				;  task ($CTCB)

;-----------------------------------------------------------------------

; Emit status and break connections.

; Input:
;   HL = status.

; This routine is called by a task via SYSENT, or indirectly via EXIT.

EMITST:	ld	c,l		; get status into BC
	ld	b,h
	ld	hl,($CTCB)	; get TCB of (maybe exiting) task into HL
EMTST:	call	BLDMSG		; build message
	ld	de,T.OCBL
	add	hl,de		; offset into OCB list
	push	hl
	ld	e,(hl)		; get address of OCB into DE
	inc	hl
	ld	d,(hl)
em1:	ld	a,d		; any (more) tasks connected?
	or	e
	jr	z,em2		; return if not
	push	de
	call	SENDST		; else send status
	pop	hl		; traverse the list
	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	dec	hl
	push	de
	ld	de,OCBSZ
	call	$FREE		; free the OCB
	pop	de
	jr	em1		; and loop for next OCB
em2:	pop	hl
	ld	(hl),a		; A is already zero!
	inc	hl
	ld	(hl),a		; clear T.OCBL
	ret

; Send status to parent task.

; Input:
;   DE = OCB address
;   TMPST = status

; If the parent task is in memory, the status is immediately stored at
; the specified ESB address. Otherwise (e.g. if checkpointed), an special
; AST block is unconditionally queued to the parent task, and the status
; stored when that AST is dispatched.

SENDST:	push	de
	pop	iy		; get OCB address into IY
	ld	c,(iy+O.PTCB)	; get TCB address of parent task into BC
	ld	b,(iy+O.PTCB+1)
	ld	e,(iy+O.EFN)	; get flag number into reg E
	push	iy
	push	bc
	call	SETFLG		; set event flag and wakeup parent
	pop	ix		; parent TCB address now in IX
	pop	iy
	ld	l,(iy+O.ESB)	; get ESB address
	ld	h,(iy+O.ESB+1)
	ld	(ESBADR),hl	; remember it
	ld	a,(iy+O.FMT)
	call	ESBSIZ		; get ESB size into BC
	bit	T2.OUT,(ix+T.ST+1) ; parent task out of memory?
	jr	nz,st1		; branch if yes

	ex	de,hl		; DE = ESB address
	ld	hl,TMPST
	call	STORST		; store status
	jr	st2

st1:	push	bc
	push	ix
	ld	hl,ACBSZ+2	; include ESB address in data area
	add	hl,bc
	ld	c,l
	ld	b,h
	call	$ALLOC		; allocate special AST block
	push	hl
	pop	iy		; get address of block into IY
	pop	ix
	pop	bc
	ret	c		; on error, return
	ld	a,c
	add	a,2
	ld	(iy+A.SIZ),a
	ld	(iy+A.TYP),AST.ST OR 80h
	ld	hl,(ESBADR)
	ld	(iy+A.DAT),l	; set ESB address
	ld	(iy+A.DAT+1),h
	push	iy
	pop	hl
	push	hl
	ld	de,A.DAT+2
	add	hl,de
	ex	de,hl		; DE = dst
	ld	hl,TMPST	; HL = src
	ldir			; set status
	ld	a,(ix+T.ASTL)	; link AST block in front of list to ensure
	ld	(iy+A.LNK),a	;  that it gets dispatched even if the task
	ld	a,(ix+T.ASTL+1)	;   has disabled AST recognition.
	ld	(iy+A.LNK+1),a
	pop	hl
	ld	(ix+T.ASTL),l
	ld	(ix+T.ASTL+1),h

st2:	ld	hl,ESBADR	; pointer to arg
	ld	de,0		; use specified AST routine address
	ld	c,2		; arg length
	ld	a,AST.ST	; AST type
	jp	QAST		; queue AST block to parent task and return

; Store status in task space.

; Input:
;   IX = TCB address
;   HL = pointer to status value
;   DE = ESB address
;   BC = size

STORST:	exx
	ld	l,(ix+T.PCB)
	ld	h,(ix+T.PCB+1)
	ld	de,P.BASE
	add	hl,de
	ld	e,(hl)		; E' = dst bank = parent task's bank
	ld	a,($SBANK)
	ld	l,a		; L' = src bank = system bank
	exx
	di
	call	$MCOPY		; store status
	ei
	ret

;-----------------------------------------------------------------------

; Build status message.

; Input:
;   BC = status
; Preserves HL.

BLDMSG:	push	hl
	ld	hl,TMPST
	ld	(hl),c		; store status
	inc	hl
	ld	(hl),b
	inc	hl
	ld	(hl),0		; set cause = 0 (for now)
	inc	hl
	ld	ix,($CTCB)
	ld	e,(ix+T.TI)
	ld	d,(ix+T.TI+1)
	call	$UCBDN
	ld	(hl),e		; set terminal device name
	inc	hl
	ld	(hl),d
	inc	hl
	ld	(hl),c
	inc	hl
	ex	de,hl
	push	ix
	pop	hl
	ld	bc,T.NAME
	add	hl,bc
	ld	bc,6
	ldir			; set task name
	ex	de,hl
	ld	a,(ix+T.ATTR)
	ld	(hl),a		; store T.ATTR
	inc	hl
	ld	a,(ix+T.ST)
	ld	(hl),a		; store T.ST
	inc	hl
	ld	a,(ix+T.ST+1)
	ld	(hl),a
	inc	hl
	ld	(hl),0		; reserved
	pop	hl
	ret

; Get status block size.

; Input:
;   A  = format
; Returns:
;   BC = ESB size
; Preserves A,DE,HL,IX,IY

ESBSIZ:	or	a		; check format
	ld	bc,2		; short
	ret	z
	ld	bc,16		; long
	ret

;-----------------------------------------------------------------------

; Disconnect from all tasks. Called when a task exits, during the
; cleanup stage.

; Input:
;   BC = task's TCB
; Preserves BC.

DISCON:	ld	iy,$RLIST
	call	disc1
	ld	iy,$WLIST
disc1:	ld	l,(iy)		; get link value into HL
	ld	h,(iy+1)
	ld	a,h		; end of list?
	or	l
	ret	z		; return if yes
	push	hl
	call	CLNOCB		; cleanup OCB list
	pop	iy
	ld	de,T.ACTL
	add	iy,de		; add link offset
	jr	disc1		; and loop to process next task

; Remove any caller's (parent) OCBs from this task

CLNOCB:	ld	de,T.OCBL
	add	hl,de
	push	hl
cln1:	pop	ix		; get head of OCB list into IX
	ld	e,(ix)
	ld	d,(ix+1)	; get pointer to OCB into DE
	ld	a,d
	or	e		; end of list?
	ret	z		; return if yes
	ld	hl,O.PTCB	; offset to TCB in OCB
	add	hl,de
	ld	a,(hl)		; check TCB field
	cp	c		; match?
	jr	nz,cln2
	inc	hl
	ld	a,(hl)
	cp	b
	jr	z,cln3		; unlink and free the OCB if yes
cln2:	push	de
	jr	cln1		; else loop to process next OCB in list

cln3:	ld	a,(de)		; get link field
	ld	(ix),a		; unlink OCB
	inc	de
	ld	a,(de)
	ld	(ix+1),a
	dec	de
	ex	de,hl		; HL = dequeued OCB
	ld	de,OCBSZ	; DE = OCB size
	push	ix
	push	bc
	call	$FREE		; free OCB
	pop	bc
  IF 0
	ld	hl,T.OCBC
	add	hl,bc
	dec	(hl)		; decrement task's outstanding OCB count
  ENDIF
	jr	cln1		; loop to process next OCB

;-----------------------------------------------------------------------

	dseg

ESBADR:	ds	2
TMPST:	ds	16

	end
