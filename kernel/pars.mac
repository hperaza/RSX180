;**********************************************************************;
;                                                                      ;
;   This file is part of RSX180, a multi-tasking OS for the Z180.      ;
;   Copyright (C) 1985-2019, Hector Peraza.                            ;
;                                                                      ;
;   This program is free software; you can redistribute it and/or      ;
;   modify it under the terms of the GNU General Public License as     ;
;   published by the Free Software Foundation; either version 2 of     ;
;   the License, or (at your option) any later version.                ;
;                                                                      ;
;   This program is distributed in the hope that it will be useful,    ;
;   but WITHOUT ANY WARRANTY; without even the implied warranty of     ;
;   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the      ;
;   GNU General Public License for more details.                       ;
;                                                                      ;
;   You should have received a copy of the GNU General Public License  ;
;   along with this program; if not, write to the Free Software        ;
;   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.          ;
;                                                                      ;
;**********************************************************************;

	NAME	('PARS')
	TITLE	'RSX180 Partition Management'

;-----------------------------------------------------------------------;
;                                                                       ;
;	Partition Management routines.                                  ;
;                                                                       ;
;-----------------------------------------------------------------------;

	.z80

	include	SYSCONF.INC
	include	PCB.INC
	include	TCB.INC
	include	ERRORS.INC

	public	NEWPAR		; create main partition control block
	public	NEWSUB		; create subpatition control block
	public	$FNPAR		; find main partition
	public	MALLOC		; allocate subpartition
	public	MEXTND		; resize subpartition
	public	MFREE		; free subpartition
	public	MAVAIL		; get amount of free memory in partition
	public	QPAR		; queue task to partition's wait list
	public	CHKPAR		; check all partitions for waiting tasks
	public	PSCAN		; assign partition to next task

	public	CPDEHL

	extrn	$ALLOC,$FREE,CLRMEM,GETSIZ,QLDR,$LKTCB
	extrn	$PLIST,$SWPRI

	cseg

;-----------------------------------------------------------------------

; Create main partition.
;
; Input:
;   B  = partition base
;   C  = partition size
;   DE = partition name.
; Returns:
;   IX = new PCB
;   CY set on error and error code in A
;
; Note: does not check for duplicate names.

NEWPAR:	push	de
	push	bc
	ld	bc,PCBSZ
	call	$ALLOC		; allocate PCB
	pop	bc
	pop	de
	ret	c
	push	hl
	pop	ix
	ld	(ix+P.BASE),b	; set partition base
	ld	(ix+P.SIZE),c	; set partition size
	ld	(ix+P.MAIN),l
	ld	(ix+P.MAIN+1),h
	push	bc
	ld	bc,P.NAME
	add	hl,bc
	ex	de,hl
	ld	bc,6
	ldir			; set partition name
	pop	bc
	xor	a
	ld	(ix+P.SUB),a	; subpartition list is initially empty
	ld	(ix+P.SUB+1),a
	ld	(ix+P.ATTR),a
	ld	(ix+P.STAT),a
	ld	(ix+P.TCB),a
	ld	(ix+P.TCB+1),a
	ld	hl,$PLIST	; keep list sorted by address
np1:	ld	e,(hl)
	inc	hl
	ld	d,(hl)
	ld	a,d
	or	e		; end of list?
	jr	z,np2		; branch if yes, link at end
	push	hl
	ld	hl,P.BASE
	add	hl,de
	ld	a,b		; get base of new PCB
	cp	(hl)		; compare with base of this PCB
	pop	hl
	jr	c,np2		; branch if this base > new base, insert here
	ex	de,hl
	jr	np1		; else loop
np2:	ld	(ix+P.LNK),e	; set next link on new PCB
	ld	(ix+P.LNK+1),d
	push	ix
	pop	de
	ld	(hl),d		; set next link on prev PCB
	dec	hl
	ld	(hl),e
	ret

; Create subpartition.
;
; Input:
;   IY = pointer to main PCB
;   E  = partition size.
; Returns:
;   IX = pointer to new sub PCB
;   CY set on error and error code in A

NEWSUB:	ld	bc,PCBSZ
	push	de
	call	$ALLOC		; allocate new sub PCB
	pop	de
	ret	c		; not enough pool
	push	hl
	pop	ix		; IX = new sub PCB
	ld	bc,P.NAME
	add	hl,bc
	ld	b,6
ma1:	ld	(hl),' '	; clear name field
	inc	hl
	djnz	ma1
	push	iy
	pop	hl
	ld	(ix+P.MAIN),l	; set pointer to main PCB
	ld	(ix+P.MAIN+1),h
	xor	a
	ld	(ix+P.SUB),a
	ld	(ix+P.SUB+1),a
	ld	(ix+P.ATTR),a
	ld	(ix+P.STAT),a
	ld	(ix+P.TCB),a
	ld	(ix+P.TCB+1),a
	set	PA.SUB,(ix+P.ATTR)
	ld	(ix+P.SIZE),e	; set size
	ret			; return success

;-----------------------------------------------------------------------

; Allocate task memory.
;
; Input:
;   IX = valid subpartition PCB
;   E  = num pages
; Returns CY on error (no space)

MALLOC:	ld	l,(ix+P.MAIN)	; get pointer to main partition
	ld	h,(ix+P.MAIN+1)
	push	hl
	pop	iy		;  into IY
	push	ix
	call	FNDGAP		; find gap, sets PRVLNK
	pop	ix
	ret	c
	set	PS.BSY,(ix+P.STAT)
	ld	(ix+P.BASE),d	; set subpartition base
	; continue below

; Link PCB.
;
; Input:
;   IX = addr of PCB to link
;   HL = addr of next PCB
;   PRVLNK = addr of 'next' field in prev PCB

LNKPCB:	ld	(ix+P.LNK),l	; keep subpartition list sorted by address
	ld	(ix+P.LNK+1),h
	push	ix
	pop	bc
	ld	hl,(PRVLNK)	; update P.SUB if prev is main,
	ld	(hl),c		;  or P.LNK if prev is sub
	inc	hl
	ld	(hl),b
	xor	a		; return success and PCB in IX
	ret

;-----------------------------------------------------------------------

; Find free gap in partition.
;
; Input:
;   IY = addr of main PCB
;   E  = size.
; Returns:
;   HL = IX = next PCB
;   PRVLNK = addr of 'next' link to update,
;   D = base of gap.
; Preserves E (size) and IY (main PCB).

FNDGAP:	bit	PA.SYS,(iy+P.ATTR)
	ld	a,E.NOMEM	; TODO: E.UPAR
	scf
	ret	z		; return error if not system-controlled

	; system-controlled partition

	push	iy
	pop	hl
	ld	bc,P.SUB
	add	hl,bc
	ld	(PRVLNK),hl	; save address of field to update

	ld	a,(iy+P.BASE)
	ld	d,a		; remember base

	ld	l,(iy+P.SUB)
	ld	h,(iy+P.SUB+1)

	push	hl
	pop	ix		; IX = sub PCB
	ld	a,h
	or	l
	jr	z,fg3		; branch if no subpartitions

	ld	bc,P.LNK

fg1:	; loop over subpartitions

	ld	a,(ix+P.BASE)	; note: 2 bytes for Z280
	sub	d		; get size of preceding gap
	cp	e		; gap big enough?
	ret	nc		; return if yes

	add	hl,bc
	ld	(PRVLNK),hl	; save address of field to update

	ld	a,(ix+P.BASE)
	add	a,(ix+P.SIZE)
	ld	d,a		; new base is after this segment

	ld	l,(ix+P.LNK)	; get pointer to next subpartition in list
	ld	h,(ix+P.LNK+1)
	push	hl
	pop	ix
	ld	a,h
	or	l
	jr	nz,fg1		; loop

	; end of subpartition list

	ld	a,(iy+P.BASE)
	add	a,(iy+P.SIZE)
	sub	d		; get size of last gap
	jr	fg4

fg3:	; no subpartitions yet

	ld	a,(iy+P.SIZE)	; gap size is main partition size
fg4:	cp	e		; big enough?
	ret	nc		; return if yes
	ld	a,E.NOMEM
	ret			; else return error (note CY set)

;-----------------------------------------------------------------------

; Free task memory. Does NOT free the PCB.
;
; Input:
;   IX = valid subpartition PCB.

MFREE:	ld	l,(ix+P.MAIN)	; get pointer to main partition
	ld	h,(ix+P.MAIN+1)
	ld	a,h
	or	l		; safety check (should not happen)
	scf			; TODO: call the system crash rotuine
	ret	z
	push	hl
	pop	iy
	; continue below	; TODO: call the system crash routine
				;  if UNLPCB fails (CY set)?

; Unlink subpartition PCB.
;
; Input:
;   IY = addr of main PCB
;   IX = addr of sub PCB
; Returns:
;   DE = IX = addr of sub PCB
;   Sets PRVLNK

UNLPCB:	push	ix
	pop	de		; DE = sub PCB

	push	iy
	pop	hl		; HL = main PCB

	ld	bc,P.SUB

mf1:	add	hl,bc
	ld	(PRVLNK),hl	; save address of field to update

	ld	a,(hl)		; get next subpartition in list
	inc	hl
	ld	h,(hl)
	ld	l,a
	or	h
	scf
	ret	z		; should not happen

	ld	bc,P.LNK
	call	CPDEHL		; same sub PCB?
	jr	nz,mf1		; loop if not

	ld	hl,(PRVLNK)
	ld	a,(ix+P.LNK)
	ld	(hl),a		; unlink
	inc	hl
	ld	a,(ix+P.LNK+1)
	ld	(hl),a
	ret			; note CY clear

; Return CY if DE > HL, Z if DE == HL.

CPDEHL:	ld	a,h
	cp	d
	ret	nz
	ld	a,l
	cp	e
	ret

;-----------------------------------------------------------------------

; Extend task memory.
;
; Input:
;   E  = num pages
;   IX = valid subpartition PCB
; Returns:
;   IX = modified PCB

MEXTND:	ld	l,(ix+P.MAIN)
	ld	h,(ix+P.MAIN+1)
	push	hl
	pop	iy
	bit	PA.SYS,(iy+P.ATTR)
	scf
	ld	a,E.NOMEM
	ret	z		; error, not a system-controlled partition
	ld	l,(ix+P.LNK)	; get next subpartition in list
	ld	h,(ix+P.LNK+1)
	ld	a,h
	or	l
	jr	nz,mx1		; branch if not the last in list

	; at end of subpartition list

	ld	d,(ix+P.BASE)
	ld	a,(iy+P.SIZE)
	sub	d		; get size of last gap
	jr	mx2

mx1:	; in middle of subpartition list

	ld	bc,P.BASE
	add	hl,bc
	ld	a,(hl)
	sub	(ix+P.BASE)	; compute max space avail to this subpartition
mx2:	cp	e		; large enough?
	jr	c,mx3		; branch if not
	ld	(ix+P.SIZE),e	; else simply adjust size
	xor	a		;  and return
	ret

mx3:	; no more space in this subpartition, try allocating a new one

	push	de
	call	UNLPCB		; unlink PCB
	pop	de
	ret	c		; should not happen
	ld	hl,(PRVLNK)
	push	hl		; save PRVLNK
	push	ix
	call	FNDGAP		; find new gap
	pop	ix
	jr	c,mx4		; branch if none found

	ld	(ix+P.BASE),d	; set new base
	ld	(ix+P.SIZE),e	;  and size
	pop	bc		; discard old PRVLNK
	call	LNKPCB		; link into new place
	ret
	
mx4:	pop	hl
	ld	(PRVLNK),hl	; restore PRVLNK
	ld	l,(ix+P.LNK)	; use old next pointer
	ld	h,(ix+P.LNK+1)
	call	LNKPCB		; restore PCB
	ld	a,E.NOMEM
	scf
	ret

;-----------------------------------------------------------------------

; Calculate available memory.
;
; Input:
;   IY = addr of main partition PCB.
; Returns:
;   HL = number of pages in HL

MAVAIL:	ld	l,(iy+P.SIZE)	; get main partition size
	ld	h,0
	bit	PA.SYS,(iy+P.ATTR)
	ret	z		; return if not system-controlled

	; system-controlled partition

	ld	e,(iy+P.SUB)
	ld	d,(iy+P.SUB+1)
mav1:	ld	a,d
	or	e
	ret	z		; return if no (more) subpartitions

	push	de
	pop	ix		; IX = sub PCB
	ld	e,(ix+P.SIZE)
	ld	d,0
	or	a
	sbc	hl,de		; subtract from main partition size

	ld	e,(ix+P.LNK)	; get pointer to next subpartition in list
	ld	d,(ix+P.LNK+1)
	jr	mav1

;-----------------------------------------------------------------------

; Find main partition.
;
; Input:
;   BC = name
; Returns:
;   HL = PCB address
;   CY set on error and error code in A

$FNPAR:	ld	hl,($PLIST)
fp1:	ld	a,h
	or	l
	scf
	ld	a,E.PNF		; main partition not found
	ret	z
	push	hl
	push	de
	push	bc
	ld	de,P.NAME
	add	hl,de
	ld	e,6
fp2:	ld	a,(bc)
	sub	(hl)
	jr	nz,fp3
	inc	hl
	inc	bc
	dec	e
	jr	nz,fp2
fp3:	pop	bc
	pop	de
	pop	hl
	ret	z		; found
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a
	jr	fp1

;-----------------------------------------------------------------------

; Queue TCB to the main partition's wait list.
;
; Input:
;   IX = TCB address

QPAR:	ld	l,(ix+T.PCB)	; get pointer to subpartition
	ld	h,(ix+T.PCB+1)
	ld	de,P.MAIN
	add	hl,de
	ld	a,(hl)
	inc	hl
	ld	h,(hl)
	ld	l,a		; HL = pointer to main partition
	ld	de,P.WAIT
	add	hl,de
	push	hl		; point to P.WAIT field
	pop	iy
	ld	bc,T.LNK	; use utility link field
	jp	$LKTCB		; link TCB to wait list
	
;-----------------------------------------------------------------------

; Scan the wait list of each main partition in the system partition list.
; For each task (of the highest priority class) competing for memory,
; determine if there is enough space in the partition. If yes, assign
; the task to the partition and queue a request to the loader to load
; the task.

CHKPAR:	ld	iy,$PLIST
ps1:	ld	c,(iy)		; get pointer to partition
	ld	b,(iy+1)
	ld	a,b
	or	c		; end of list?
	ret	z		; return if yes
	push	bc
	push	bc
	pop	iy
	call	PSCAN		; else check this partition's wait list
	pop	iy
	jr	ps1		; loop to process next

; Assign partition to the next waiting task.

PSCAN:	bit	PA.SYS,(iy+P.ATTR) ; system-controlled partition?
	jr	nz,ps2		   ; branch if yes

	bit	PS.BSY,(iy+P.STAT) ; partition busy?
	ret	nz		   ; return if yes

	ld	l,(iy+P.WAIT)	; get pointer to 1st TCB in wait list
	ld	h,(iy+P.WAIT+1)
	ld	a,h
	or	l		; any tasks waiting on this partition?
	ret	z		; return if not

	push	hl
	pop	ix		; IX = 1st TCB

	call	GETSIZ		; compute task size
;;	jr	c,...		; abort task: will not fit in memory
;;				; (should have been validated by INS or VMR,
;;				;  will cause a LDR 'Load Error')

;;	ld	a,(iy+P.SIZE)
;;	cp	e
;;	jr	c,...		; abort task: will not fit in partition
;;				; (should have been validated by INS or VMR,
;;				;  will cause a LDR 'Load Error')

	set	PS.BSY,(iy+P.STAT)

	push	iy
	pop	bc
	ld	(ix+T.PCB),c	; assign partition to the task
	ld	(ix+T.PCB+1),b
	push	ix
	pop	bc
	ld	(iy+P.TCB),c	; assign task to the partition
	ld	(iy+P.TCB+1),b

	ld	a,(ix+T.LNK)
	ld	(iy+P.WAIT),a	; remove TCB from the partition wait list
	ld	a,(ix+T.LNK+1)
	ld	(iy+P.WAIT+1),a

	ld	a,($SWPRI)
	ld	(ix+T.SPRI),a	; reset swap priority to maximum
	call	QLDR		; queue task to loader (T2.OUT and T2.CKP
	ret			;  are already set accordingly)

ps2:	ld	de,P.WAIT
	add	iy,de		; IY = addr of 'prev' field for unlink op
ps3:	ld	c,(iy)		; get pointer to 1st TCB in wait list
	ld	b,(iy+1)
	ld	a,b
	or	c		; any tasks waiting on this partition?
	ret	z		; return if not

ps31:	push	bc
	pop	ix		; IX = TCB address

	ld	a,(ix+T.PRI)
	ld	(TPRI),a	; remember first task's priority

ps4:	ld	a,(ix+T.ST+1)
	res	T2.OUT,a
	res	T2.CKP,a
	or	a		; task runnable?
	jr	z,ps41		; branch if yes
				;  else skip it (TODO: skip this test if
				;  the task is checkpointed and the
				;  checkpoint partition is being discontinued)

	ld	c,(ix+T.LNK)	; get next TCB in wait list
	ld	b,(ix+T.LNK+1)
	ld	a,b
	or	c		; end of list?
	ret	z		; return if yes

	push	ix
	pop	iy		; get addr 'prev' field into IY
;	ld	de,T.LNK	; we *know* T.LNK is 0
;	add	iy,de		;
;;	lea	iy,(ix+T.LNK)	; Z280
	jr	ps31

ps41:	ld	e,(ix+T.PCB)
	ld	d,(ix+T.PCB+1)
	push	de
	ex	(sp),ix		; push TCB, pop sub PCB
	push	iy
	ld	e,(ix+P.SIZE)
	call	MALLOC		; try to allocate memory for task
	pop	iy
	pop	ix		; pop TCB address
	jr	c,ps5		; skip if not enough memory

	ld	a,(ix+T.LNK)
	ld	(iy),a		; unlink TCB from partition wait list
	ld	a,(ix+T.LNK+1)
	ld	(iy+1),a

	push	iy
	ld	a,($SWPRI)
	ld	(ix+T.SPRI),a	; reset swap priority to maximum
	call	QLDR		; queue task to loader (T2.OUT is already set
	pop	iy		;  and T2.CKP is set if task is checkpointed)
	jr	ps3		; loop to process next task,
				;  resetting priority class

ps5:	IF	F$CHKP
	push	ix
	push	iy		; try to make space by checkpointing
	call	MKSPC		;  lower priority tasks
	pop	iy
	pop	ix
	ENDIF

	ld	c,(ix+T.LNK)	; get next TCB in wait list
	ld	b,(ix+T.LNK+1)
	ld	a,b
	or	c		; end of list?
	ret	z		; return if yes

	push	ix
	pop	iy		; get addr 'prev' field into IY
;	ld	de,T.LNK	; we *know* T.LNK is 0
;	add	iy,de		;
;;	lea	iy,(ix+T.LNK)	; Z280

	push	bc
	pop	ix

	ld	a,(TPRI)
	cp	(ix+T.PRI)	; same priority class?
	jr	z,ps4		; loop to process task if yes
	ret			; else we're done

;-----------------------------------------------------------------------

; *** Checkpoint partition section ***

	IF	F$CHKP

	public	ALLCS		; allocate checkpoint space
	public	FREECS		; free checkpoint space
	public	$CHKPT		; initiate task checkpoint

	extrn	$CPLST,$RLIST,$WLIST,UNLTCB

;-----------------------------------------------------------------------

; Allocate space within one of the checkpoint files.
;
; Input:
;   DE = size in disk blocks
; Returns:
;   IX = addr of new checkpoint sub PCB
;   IY = addr of main checkpoint PCB
;   CY set on error (no space)

ALLCS:	ld	hl,($CPLST)	; get head of checkpoint partition list
acs1:	ld	a,h
	or	l		; end of list?
	ld	a,E.NOMEM	; checkpoint allocation failure
	scf
	ret	z		; return error if yes

	push	hl
	pop	iy
	ld	a,(iy+P.REL)
	and	(iy+P.REL+1)
	inc	a		; checkpoint file being discontinued?
	jr	z,acs2		; skip it if yes

	call	CKPGAP		; find gap, sets PRVLNK and RELBLK
	jr	nc,acs3		; branch on success

acs2:	ld	l,(iy+P.LNK)
	ld	h,(iy+P.LNK+1)
	jr	acs1		; else loop to check next PCB in list

acs3:	ld	bc,CPCBSZ
	push	hl
	push	de
	call	$ALLOC		; allocate new sub PCB
	pop	de
	pop	bc
	ret	c		; not enough pool
	push	hl
	pop	ix		; IX = new sub PCB
	push	iy
	pop	hl
	ld	(ix+P.MAIN),l	; set pointer to main PCB
	ld	(ix+P.MAIN+1),h
	ld	a,(iy+P.UCB)
	ld	(ix+P.UCB),a	; set UCB pointer
	ld	a,(iy+P.UCB+1)
	ld	(ix+P.UCB+1),a
	ld	a,(iy+P.LBN)	; set starting LBN of checkpoint file
	ld	(ix+P.LBN),a
	ld	a,(iy+P.LBN+1)
	ld	(ix+P.LBN+1),a
	ld	a,(iy+P.LBN+2)
	ld	(ix+P.LBN+2),a
	ld	a,(iy+P.LBN+3)
	ld	(ix+P.LBN+3),a
	xor	a
	ld	(ix+P.LNK),a
	ld	(ix+P.LNK+1),a
	ld	(ix+P.SUB),a
	ld	(ix+P.SUB+1),a
	ld	l,c
	ld	h,b
	ld	bc,(RELBLK)
	ld	(ix+P.REL),c	; set subpartition relative block number
	ld	(ix+P.REL+1),b
	ld	(ix+P.SIZE),e	; set size
	ld	(ix+P.SIZE+1),d
	jp	LNKPCB		; link PCB

; Find free gap in checkpoint partition.
;
; Input:
;   IY = main checkpoint PCB
;   DE = size in disk blocks
; Returns:
;   HL = IX = next PCB
;   PRVLNK = addr of 'next' link to update
;   RELBLK = relative block number
; Preserves DE (size) and IY (main PCB)

CKPGAP:	push	iy
	pop	hl
	ld	bc,P.SUB
	add	hl,bc
	ld	(PRVLNK),hl	; save address of field to update

	ld	hl,0
	ld	(RELBLK),hl	; start from the beginning of the file

	ld	l,(iy+P.SUB)
	ld	h,(iy+P.SUB+1)

	push	hl
	pop	ix		; IX = sub PCB
	ld	a,h
	or	l
	jr	z,cg3		; branch if no subpartitions

	ld	bc,P.LNK

cg1:	; loop over subpartitions

	push	hl
	push	de
	ld	l,(ix+P.REL)
	ld	h,(ix+P.REL+1)
	ld	de,(RELBLK)
	or	a
	sbc	hl,de		; get size of preceding gap
	pop	de
	call	CPDEHL		; gap big enough? NC  HL siz <=DE gap
	pop	hl
	ret	nc		; return if yes

	add	hl,bc
	ld	(PRVLNK),hl	; save address of field to update

	push	hl
	push	de
	ld	l,(ix+P.REL)
	ld	h,(ix+P.REL+1)
	ld	e,(ix+P.SIZE)
	ld	d,(ix+P.SIZE+1)
	add	hl,de
	ld	(RELBLK),hl	; new relative block is after this segment
	pop	de
	pop	hl

	ld	l,(ix+P.LNK)	; get pointer to next subpartition in list
	ld	h,(ix+P.LNK+1)
	push	hl
	pop	ix
	ld	a,h
	or	l
	jr	nz,cg1		; loop

	; end of subpartition list

	push	de
	ld	l,(iy+P.SIZE)
	ld	h,(iy+P.SIZE+1)
	ld	de,(RELBLK)
	sbc	hl,de		; get size of last gap (CY clear from above)
	pop	de
	jr	cg4

cg3:	; no subpartitions yet

	ld	l,(iy+P.SIZE)	; gap size is main partition size
	ld	h,(iy+P.SIZE+1)
cg4:	call	CPDEHL		; big enough?
	push	ix
	pop	hl
	ret	nc		; return if yes
	ld	a,E.NOMEM
	ret			; else return error (note CY set)

;-----------------------------------------------------------------------

; Free checkpoint space. Does NOT free the PCB.
;
; Input:
;   IX = valid subpartition PCB

FREECS:	ld	l,(ix+P.MAIN)	; get pointer to main partition
	ld	h,(ix+P.MAIN+1)
	ld	a,h
	or	l		; safety check (should not happen)
	scf			; TODO: call the system crash rotuine
	ret	z
	push	hl
	pop	iy
	call	UNLPCB		; TODO: call the system crash routine
	ret			;  if UNLPCB fails (CY set)?

;-----------------------------------------------------------------------

; Initiate task checkpoint.
;
; Input:
;   IX = TCB address

$CHKPT:	bit	T2.CKP,(ix+T.ST+1)
	scf
	ld	a,E.BADOP
	ret	nz		; already checkpointed

	bit	TA.FIX,(ix+T.ATTR)
	scf
	ret	nz		; task fixed

	bit	TA.CKD,(ix+T.ATTR)
	scf
	ret	nz		; checkpointing disabled

	bit	T2.ABO,(ix+T.ST+1)
	scf
	ret	nz		; task being aborted

	ld	a,(ix+T.IOC)
	or	a
	jr	z,ckp1

	set	TS.CKR,(ix+T.ST); task has outstanding I/O, request checkpoint
	xor	a
	ret

ckp1:	ld	l,(ix+T.PCB)
	ld	h,(ix+T.PCB+1)
	ld	de,P.SIZE
	add	hl,de
	ld	l,(hl)		; get task size in pages
	ld	h,0
	add	hl,hl		; obtain size in disk blocks
	add	hl,hl
	add	hl,hl
	ex	de,hl		; DE = required checkpoint file blocks
	push	ix		; save TCB address
	call	ALLCS		; allocate checkpoint file space
	ex	(sp),ix		; restore TCB address
	pop	bc		; BC = addr of checkpoint PCB
	ret	c		; TODO: abort task?

	ld	(ix+T.CPCB),c	; set pointer to checkpoint subPCB in TCB
	ld	(ix+T.CPCB+1),b
	set	T2.CKP,(ix+T.ST+1)
	res	TS.CKR,(ix+T.ST)

	push	ix
	pop	bc
	ld	iy,$RLIST
	ld	de,T.ACTL
	call	UNLTCB
	jr	c,ckp2
	ld	iy,$WLIST
	ld	bc,T.ACTL
	call	$LKTCB		; move task to wait list
ckp2:	jp	QLDR		; queue task to loader

; Try to make space for the new task by checkpointing one (or several
; adjacent) tasks in memory.

; Input:
;   IX = TCB addr of contender task

MKSPC:	ld	c,(ix+T.PCB)
	ld	b,(ix+T.PCB+1)
	ld	hl,P.SIZE
	add	hl,bc
	ld	a,(hl)
	ld	(SIZE),a	; save requested size
	ld	hl,P.MAIN
	add	hl,bc
	ld	c,(hl)		; get pointer to main partition
	inc	hl
	ld	b,(hl)
	ld	(FIRST),bc
	push	bc
	pop	iy
	ld	d,(iy+P.BASE)	; get base of first span to consider
	ld	l,(iy+P.SUB)	; get address of first subPCB
	ld	h,(iy+P.SUB+1)
mk1:	ld	a,h
	or	l
	scf
	ret	z		; end of list, return (nothing found)

	push	hl		; get PCB address
	pop	iy		;  into IY
	ld	c,(iy+P.TCB)	; get TCB address of owner task
	ld	b,(iy+P.TCB+1)
	call	CKPTST		; can it be checkpointed?
	jp	c,mk2		; skip it if not

	ld	e,0
	ld	c,(iy+P.LNK)	; get address of next PCB
	ld	b,(iy+P.LNK+1)
	ld	a,b
	or	c
	jr	nz,mk4
	ld	c,(iy+P.MAIN)	; that was the last, see what's left
	ld	b,(iy+P.MAIN+1)
	ld	hl,P.SIZE
	add	hl,bc
	ld	a,e
	add	(hl)
	ld	e,a
mk4:	ld	hl,P.BASE
	add	hl,bc
	ld	a,e
	add	(hl)
	sub	d
	ld	e,a
	ld	hl,SIZE
	cp	(hl)		; enough space?  CY if SIZE > E
	jr	c,mk3		; branch if not

	ld	hl,(FIRST)
	push	hl
	ex	(sp),iy
	ld	e,(iy+P.MAIN)
	ld	d,(iy+P.MAIN+1)
	call	CPDEHL
	jr	nz,mk6
	ld	l,(iy+P.SUB)	; addr of first PCB to checkpoint
	ld	h,(iy+P.SUB+1)
	jr	mk7
mk6:	ld	l,(iy+P.LNK)
	ld	h,(iy+P.LNK+1)
mk7:	push	hl
	ld	bc,P.TCB
	add	hl,bc
	ld	c,(hl)		; get TCB address of owner task
	inc	hl
	ld	b,(hl)
	push	bc
	pop	ix
	call	$CHKPT		; initiate checkpoint
	pop	de
	pop	hl
	call	CPDEHL		; end of span reached?
	ret	z		; return success if yes
	push	de
	pop	iy
	push	hl
	jr	mk6		; else loop to checkpoint next

mk2:	ld	a,(iy+P.BASE)
	add	(iy+P.SIZE)
	ld	d,a		; base address of next span to consider
	ld	(FIRST),iy	; first PCB of next span
mk3:	ld	l,(iy+P.LNK)
	ld	h,(iy+P.LNK+1)
	jp	mk1		; loop

; See if we can checkpoint this task

; Input:
;   BC = TCB address of task to checkpoint
;   IX = TCB address of contender task

CKPTST:	ld	hl,T.ATTR
	add	hl,bc
	bit	TA.FIX,(hl)
	scf
	ret	nz		; no can do: task is fixed

	bit	TA.CKD,(hl)
	scf
	ret	nz		; checkpoint disabled

	ld	hl,T.ST+1
	add	hl,bc
	bit	T2.ABO,(hl)
	scf
	ret	nz		; task being aborted, keep in memory

	xor	a
	bit	T2.STP,(hl)	; task stopped?
	ret	nz		; return if yes, effective priority is zero

	push	bc
	ex	(sp),iy
	push	de
	ld	a,(iy+T.SPRI)
	ld	l,a
	rla			; sign extend
	sbc	a,a
	ld	h,a
	ld	e,(iy+T.PRI)
	ld	d,0
	or	a
	adc	hl,de		; compute effective priority
	jp	m,t1		; if negative, checkpoint task
	ld	a,l
	cp	(ix+T.PRI)	; compare with priority of contender task
	ccf			;  if lower, checkpoint task
	jr	c,t2
t1:	xor	a
t2:	pop	de
	pop	iy
	ret

	ENDIF

;-----------------------------------------------------------------------

	dseg

PRVLNK:	ds	2	; link to prev PCB in list
TPRI:	ds	1	; task priority class for partition wait list scan
	IF	F$CHKP
RELBLK:	ds	2	; relative block number within checkpoint file
FIRST:	ds	2
SIZE:	ds	1
	ENDIF

	end
