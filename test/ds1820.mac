	.Z180

	ident	'V1.1'

	include	QIO.INC
	include	SYSFN.INC
	include	ERRORS.INC

CR	equ	0Dh
LF	equ	0Ah

DSLUN	equ	2

CE.DNF	equ	1
CE.CRC	equ	2
CE.TMO	equ	3

	extrn	ADEC,AHEX,PUTCH,PUTSTR,PERROR,CRLF,EXIT

;--------------------------------------------------------------------

	cseg

; Read the temperature from a DS1820 chip using the custom CT: driver.

START:	ld	sp,stack

	ld	de,'CT'		; CT0:
	ld	c,0
	ld	b,DSLUN
	SC	.ALUN
	jp	c,ERROR

	ld	hl,DSQIO
	SC	.QIO		; read temperature
	jp	c,ERROR

	ld	a,(RESULT)
	or	a
	jp	nz,ERROR

	call	CRLF

	ld	hl,SRMSG
	call	PUTSTR
	ld	a,(IOBUF)
	call	AHEX		; output family code
	ld	c,'-'
	call	PUTCH
	ld	hl,IOBUF+6
	ld	b,6		; output 48-bit serial number, MSB first
L3:	ld	a,(hl)
	call	AHEX
	dec	hl
	djnz	L3
	call	CRLF
	ld	hl,TMSG
	call	PUTSTR
	push	hl
	call	DSTEMP		; output temperature
	pop	hl
	inc	hl
	call	PUTSTR
	ld	c,CR
	call	PUTCH
	ld	hl,EX.SUC
	jp	EXIT

SRMSG:	db	'DS1820 chip found with serial number ',0
TMSG:	db	'The temperature is ',0,' degrees Celsius',0

; Display the measured temperature

DSTEMP:	ld	a,(DSSIGN)
	or	a		; is the sign positive?
	ld	a,(DSMAG)
	ld	c,'+'		; yes, display a plus sign
	jr	z,DT1
	neg			; otherwise, take the complement of DSMAG
	ld	c,'-'		; and display a minus sign
DT1:	ld	b,a
	call	PUTCH
	ld	a,b
	or	a
	rra			; send the .5 deg bit to CY
	push	af
	call	ADEC
	pop	af
	ret	nc
	ld	c,'.'		; display .5 if necessary
	call	PUTCH
	ld	c,'5'
	call	PUTCH
	ret

; Display error message

ERROR:	push	af
	call	CRLF
	pop	af
	cp	E.FAIL
	jr	nz,err1
	ld	c,a
	ld	a,(RESULT+1)	; check extended error code
	cp	CE.DNF
	ld	hl,NO1820
	jr	z,err2
	cp	CE.CRC
	ld	hl,XFERR
	jr	z,err2
	cp	CE.TMO
	ld	hl,DSTMO
	jr	z,err2
	ld	a,c
err1:	call	PERROR
	jr	err3
err2:	call	PUTSTR
err3:	ld	c,CR
	call	PUTCH
	ld	hl,EX.ERR
	jp	EXIT

NO1820:	db	'DS1820 chip not found',0
XFERR:	db	'CRC error during data transfer',0
DSTMO:	db	'Timeout in response from DS1820 chip',0

;--------------------------------------------------------------------

	dseg

DSQIO:	QIOW$	IO.RLB,DSLUN,3,RESULT,,<IOBUF,9,0,,,>

RESULT:	ds	4

IOBUF::	ds	9
DSSIGN	equ	IOBUF+7		; DSSIGN is 0 if temp is positive, FF if neg.
DSMAG	equ	IOBUF+8		; DSMAG contains the 8 bit temperature reading,
				; LSB = .5 degrees.

	ds	256
STACK	equ	$

	end	START
