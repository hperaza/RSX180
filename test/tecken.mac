	.Z80

	TITLE	TECKEN
	IDENT	/X0.0/

	include	SYSFN.INC
	include	QIO.INC
	include	DCB.INC
	include	ERRORS.INC

CR	equ	0Dh
LF	equ	0Ah
TAB	equ	09h

	extrn	CVTWD,UCASE

	CSEG
;
START::	ld	sp,stack
	ld	hl,qio1
	SC	.QIO
	ld	hl,qio10
	SC	.QIO
	ld	hl,qio2
	SC	.QIO
	xor	a
	ld	(QPOS),a
LOOP:	ld	hl,qio3
	SC	.QIO
	ld	hl,BUF2
	ld	a,(BUF)
	ld	e,a
	ld	d,0
	ld	a,' '
	call	CVTWD
	ld	(hl),TAB
	inc	hl
	ld	de,MSG
	or	a
	sbc	hl,de
	ld	(qio4+Q.LEN),hl
	ld	hl,qio4
	SC	.QIO
	ld	a,(BUF)
	cp	' '+1
	jr	c,CTL
	cp	7Fh
	jr	c,COK
	cp	0A0h+1
	jr	c,CSPEC
COK:	ld	hl,qio5
	SC	.QIO
	jr	DONE
CSPEC:	sub	5Eh
CTL:	add	a,a
	add	a,a
	ld	e,a
	ld	d,0
	ld	hl,ASCII
	add	hl,de
	ld	(qio6+Q.BUF),hl
	ld	hl,qio6
	SC	.QIO
DONE:	ld	hl,qio7
	SC	.QIO
CHKEND:	ld	a,(QPOS)
	ld	e,a
	ld	d,0
	ld	hl,QSTR
	add	hl,de
	ld	a,(BUF)
	and	7Fh
	call	UCASE
	cp	(hl)
	jr	z,MATCH
	ld	a,e
	or	a
	jr	z,LOOP
	xor	a
	ld	(QPOS),a
	jr	CHKEND
MATCH:	inc	hl
	ld	a,(hl)
	or	a
	jr	z,end
	ld	a,e
	inc	a
	ld	(QPOS),a
	jp	LOOP
END::	ld	hl,qio8
	SC	.QIO
	ld	hl,qio9
	SC	.QIO
	ld	hl,EX.SUC
	SC	.EXIT

	DSEG

IO.RAL	equ	IO.RLB OR (TF.RAL SHL 8)
IO.RNE	equ	IO.RLB OR (TF.RNE SHL 8)
qio1:	QIOW$	IO.ATT,5,1,iostat
qio10:	QIOW$	SF.SMC,5,1,iostat,,<ttchr,2>
qio2:	QIOW$	IO.WLB,5,1,iostat,,<HELO,HELOL>
qio3:	QIOW$	<<IO.RNE OR IO.RAL>>,5,1,iostat,,<BUF,1>
qio4:	QIOW$	IO.WLB,5,1,iostat,,<MSG,>
qio5:	QIOW$	IO.WLB,5,1,iostat,,<BUF,1>
qio6:	QIOW$	IO.WLB,5,1,iostat,,<,3>
qio7:	QIOW$	IO.WLB,5,1,iostat,,<HELO,2>
qio8:	QIOW$	IO.WLB,5,1,iostat,,<EMSG,EMSGL>
qio9:	QIOW$	IO.DET,5,1,iostat
iostat:	defs	4
ttchr:	db	TC.BIN,1

ASCII:	defb	'NUL SOH STX ETX EOT ENQ ACK BEL BS  HT  LF  VT  FF  CR  '
	defb	'SO  SI  DLE DC1 DC2 DC3 DC4 NAK SYN ETB CAN EM  SUB ESC '
	defb	'FS  GS  RS  US  SP  '
	defb	'DEL 80  81  82  83  IND NEL SSA ESA HTS HTJ VTS PLD PLU RI  '
	defb	'SS2 SS3 DCS PU1 PU2 STS CRH MW  SPA EPA 98  99  9A  CSI '
	defb	'ST  OSC PM  APC NSP '

HELO:	defb	CR,LF,'Character check. End with "exit"<CR>',CR,LF
HELOL	equ	$-HELO

EMSG:	defb	CR,LF,'Program finished.',CR
EMSGL	equ	$-EMSG

BUF:	defs	2

MSG:	defb	'Char:  '
BUF2:	defs	8

QSTR:	defb	'EXIT',CR,0
QPOS:	defs	1

	defs	100
stack	equ	$

	END	START
